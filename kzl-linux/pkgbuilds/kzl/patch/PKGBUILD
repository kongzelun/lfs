# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=patch
pkgver=2.7.6+17+g9c98635
pkgrel=1
epoch=1
pkgdesc="A utility to apply patch files to original sources"
arch=('x86_64')
url="https://www.gnu.org/software/patch/"
license=('KZL')
depends=(attr)
makedepends=(ed)
optdepends=("ed: for patch -e functionality")
source=(git+git://git.savannah.gnu.org/patch.git#commit=9c986353e420ead6e706262bf204d6e03322c300)
sha256sums=('SKIP')
validpgpkeys=('259B3792B3D6D319212CC4DCD5BF9FEB0313653A')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd $pkgname

  ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
