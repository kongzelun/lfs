# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libffi
pkgver=3.4.2
pkgrel=1
epoch=1
pkgdesc="Portable foreign function interface library"
arch=('x86_64')
url="https://sourceware.org/libffi/"
license=('KZL')
source=(https://github.com/libffi/libffi/releases/download/v$pkgver/libffi-$pkgver.tar.gz)
sha256sums=('540fb721619a6aba3bdeef7d940d8e9e0e6d2c193595bc243241b77ff9e93620')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -ifv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-pax_emutramp

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
