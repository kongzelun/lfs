# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=smartmontools
pkgver=7.2
pkgrel=1
epoch=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
arch=('x86_64')
url="http://smartmontools.sourceforge.net"
license=('KZL')
optdepends=("s-nail: to get mail alerts to work")
backup=(etc/smartd.conf
        etc/conf.d/smartd)
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        smartd.conf)
sha256sums=('5cd98a27e6393168bc6aaea070d9e1cd551b0f898c52f66b2ff2e5d274118cd6'
            'SKIP'
            'e153771c1879889f2d32312d5728dce145c4ff044143ba87b1efbd0572544eb9')
validpgpkeys=('847AF8F72DF4C7B492382C94BF0B339C64BCAA8B')

prepare() {
  cd $pkgname-$pkgver

  # use `uname -n` instead of `hostname`
  sed -e "/^os_hostname/c os_hostname=\"'uname -n'\"" -i configure.ac

  # smartd.service.in: Remove obsolete 'StandardOutput=syslog'
  # https://github.com/smartmontools/smartmontools/commit/f73303bc2d40adfc71e8bfaae043275e88cf9275
  sed -i '/^StandardOutput=/d' smartd.service.in

  autoreconf -ifv
}

###############################################################################

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-drivedbdir=yes \
    --with-smartdscriptdir=/usr/share/smartmontools \
    --with-smartdplugindir=/usr/share/smartmontools/smartd_warning.d \
    --with-libcap-ng=yes \
    --with-libsystemd=yes \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-systemdenvfile=/etc/conf.d/smartd \

  make
}

package() {
  cd $pkgname-$pkgver

  sed -i -e "s:sysconfig/smartmontools:conf.d/smartd:g" smartd.service
  sed -i -e "s:smartd_opts:SMARTD_ARGS:g" smartd.service

  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir"/etc/rc.d
  install -Dm644 "$srcdir"/smartd.conf "$pkgdir"/etc/conf.d/smartd
}
