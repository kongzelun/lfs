# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=db
pkgver=5.3.28
pkgrel=1
epoch=1
pkgdesc="The Berkeley DB embedded database system"
arch=('x86_64')
url="https://www.oracle.com/technology/software/products/berkeley-db/index.html"
source=(https://download.oracle.com/berkeley-db/db-$pkgver.tar.gz
        atomic.patch)
sha256sums=('e0a992d740709892e81f9d93f06daf305cf73fb81b545afe72478043172c3628'
            'ba0e2b4f53e9cb0ec58f60a979b53b8567b4565f0384886196f1fc1ef111d151')

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -p0 -i ../atomic.patch
}

build() {
  cd $pkgname-$pkgver/build_unix

  ../dist/configure \
    --prefix=/usr \
    --enable-cxx \
    --enable-compat185 \
    --enable-stl \
    --enable-dbm \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd $pkgname-$pkgver/build_unix

  make DESTDIR="$pkgdir/" install

  rm -r "$pkgdir"/usr/docs
}
