# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=hwloc
pkgver=2.7.0
pkgrel=1
epoch=1
pkgdesc="(Portable Hardware Locality) a portable abstraction of hierarchical architectures"
arch=('x86_64')
url="https://www.open-mpi.org/projects/hwloc/"
license=('KZL')
makedepends=(cairo libxml2 pciutils)
optdepends=(cairo libxml2 pciutils)
source=(https://www.open-mpi.org/software/hwloc/v${pkgver%.*}/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('028cee53ebcfe048283a2b3e87f2fa742c83645fc3ae329134bf5bb8b90384e0')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -ifv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-plugins

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
