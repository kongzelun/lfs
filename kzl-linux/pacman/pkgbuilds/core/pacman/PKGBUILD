# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman
pkgver=7.0.0.g21
pkgrel=1
epoch=
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('KZL')
depends=(
  bash
  curl
  fakeroot
  glibc
  # gpgme
  libarchive
  libseccomp
)
# optdepends=("archlinux-keyring: for Arch Linux official packages")
backup=(
  etc/makepkg.conf
  etc/makepkg.conf.d/rust.conf
  etc/pacman.conf
)
source=(git+https://gitlab.archlinux.org/pacman/pacman.git)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/^v//;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  sed -i \
      -e '/^#CPPFLAGS=/c\CPPFLAGS=""' \
      -e '/^#CFLAGS=/c\CFLAGS="-march=native -O2 -pipe -fno-plt -fexceptions -fstack-clash-protection -fcf-protection"' \
      -e '/^#CXXFLAGS=/c\CXXFLAGS="$CFLAGS -Wp,-D_GLIBCXX_ASSERTIONS"' \
      -e '/^#LDFLAGS=/c\LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"' \
      -e '/^#MAKEFLAGS=/c\MAKEFLAGS="-j$(nproc) -O"' \
      -e '/^#DEBUG_CFLAGS=/c\DEBUG_CFLAGS="-g -fvar-tracking-assignments"' \
      -e '/^#DEBUG_CXXFLAGS=/c\DEBUG_CXXFLAGS="-g -fvar-tracking-assignments"' \
      -e '/^#BUILDDIR=/c\BUILDDIR="$HOME/makepkg/build"' \
      -e '/^INTEGRITY_CHECK=/s/(.*)/(sha256)/' \
      -e '/^#PKGDEST=/c\PKGDEST="$HOME/makepkg/packages"' \
      -e '/^#SRCDEST=/c\SRCDEST="$HOME/makepkg/sources"' \
      -e '/^#SRCPKGDEST=/c\SRCPKGDEST="$HOME/makepkg/srcpackages"' \
      -e '/^#LOGDEST=/c\LOGDEST="$HOME/makepkg/makepkglogs"' \
      -e '/^#PACKAGER=/c\PACKAGER="Zelun Kong <zelun.kong@outlook.com>"' \
      -e '/^OPTIONS=/s/libtool/!libtool/' \
      -e '/^OPTIONS=/s/!autodeps/autodeps/' \
      etc/makepkg.conf.in

  sed -i \
      -e '/^#RUSTFLAGS=/s/#//' \
      -e '/^#DEBUG_RUSTFLAGS=/s/#//' \
      etc/makepkg.conf.d/rust.conf.in

  cat >> etc/pacman.conf.in << EOF

[core]
SigLevel = Optional TrustAll
Server = file:///home/.repository/\$repo
EOF

  meson setup \
    --prefix /usr \
    --optimization 2 \
    -D b_lto=true \
    -D b_pie=true \
    -D buildstatic=false \
    -D crypto=openssl \
    -D curl=enabled \
    -D doc=disabled \
    -D doxygen=disabled \
    -D file-seccomp=enabled \
    -D gpgme=disabled \
    -D ldconfig=/usr/bin/ldconfig \
    -D pkg-ext='.pkg.tar.zst' \
    -D scriptlet-shell=/usr/bin/bash \
    -D src-ext='.src.tar.zst' \
    -D use-git-version=false \
    build
}

build() {
  cd $pkgname

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build
}

package() {
  cd $pkgname

  meson install -C build --destdir="$pkgdir"

  rm -rf "$pkgdir"/usr/share
}
