# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=filesystem
pkgver=20230622
pkgrel=1
epoch=
pkgdesc="Root file system"
arch=('any')
license=('KZL')
depends=(iana-etc)
backup=(etc/crypttab
        etc/fstab
        etc/group
        etc/gshadow
        etc/host.conf
        etc/hosts
        etc/motd
        etc/passwd
        etc/profile
        etc/resolv.conf
        etc/securetty
        etc/shadow
        etc/shells
        etc/subgid
        etc/subuid)
source=(shadow-convert.sh
        generate-sysusers.sh
        crypttab
        fstab
        group
        host.conf
        hosts
        motd
        passwd
        profile
        resolv.conf
        securetty
        shells
        subgid
        subuid)
sha256sums=('75e6064eaa43505c8e9ed31850633279857cc678d733571b5bfe636c847b4369'
            '06f0b3dd5c3116575595d94458c2ec967e538e8cd0b930d86d3619d374a2eac9'
            'a6f0ad7acc6054199fbab680a53769a936a4518a2ee751404839b77fb2a4be79'
            '63ea61478aeaab0640ac76d915e94ddf3a0c8a661ee1188c8365536141c7afc2'
            '12e361821edc8bb813b7c38b11f61aa39c262e6b35bb2462896cb51edc821e2d'
            '4d7b647169063dfedbff5e1e22cee77bd1a4183dbcfd5e802e68939da4bbf733'
            'd9cd8a77d9e0aa5e90d7f4ed74c8745c17b525e720e28e4c44364150003c35f9'
            '404c3f2e1d3d4097cedd08a4484119cb37d9adfc0ead49bdf5960e227e9159de'
            '2484582c0044ccbe58601ce437cf51b672de4b6ea743ab4d4223ddf2ce0afb68'
            '5d2cb175897062d889c4b2f210a3514fab06abc2aeb8dbb545b1236c58ac37a8'
            '5557d8e601b17a80d1ea7de78a9869be69637cb6a02fbfe334e22fdf64e61d4c'
            'd88be2b45b43605ff31dd83d6a138069b6c2e92bc8989b7b9ab9eba8da5f8c7b'
            'dd8d9d43fbe6925149c1635728e6570168ffc91fb8fabeb46fb4f6f115f389d5'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855')

pkgver() {
  date +%Y%m%d
}

build() {
  ./shadow-convert.sh
  ./generate-sysusers.sh
}

package() {
  cd $pkgdir

  # root filesystem
  install -m 755 -d {boot,dev,etc,home,mnt,usr,var,opt,srv,run}
  install -m 0750 -d root
  install -m 555 -d {proc,sys}
  install -m 1777 -d tmp

  # /etc
  install -m 755 -d etc/{skel,profile.d} usr/share/factory/etc
  for f in fstab group host.conf hosts motd passwd profile resolv.conf securetty shells subgid subuid; do
    install -D -m 644 "$srcdir/$f" etc/
    install -D -m 644 "$srcdir/$f" usr/share/factory/etc/
  done
  ln -s ../proc/self/mounts etc/mtab
  for f in crypttab gshadow shadow; do
    install -D -m 600 "$srcdir/$f" etc/
    install -D -m 600 "$srcdir/$f" usr/share/factory/etc/
  done

  # /usr
  install -m 755 -d usr/{bin,sbin,include,lib,libexec,share,src}
  install -m 755 -d usr/share/man/man{1..8}
  ln -s usr/lib lib
  ln -s usr/lib lib64
  ln -s lib usr/lib64
  ln -s usr/bin bin
  ln -s usr/sbin sbin

  # /usr/local
  install -m 755 -d usr/local/{bin,sbin,etc,include,lib,libexec,share,src}

  # var
  install -m 755 -d var/{cache,crash,lib,local,log,mail,opt,spool}
  install -m 1777 -d var/{crash,tmp}
  ln -s ../mail var/spool/mail

  # setup default system groups/users
  install -D -m644 "$srcdir"/20-basic-groups.conf -t "$pkgdir"/usr/lib/sysusers.d/
  install -D -m644 "$srcdir"/20-basic-users.conf -t "$pkgdir"/usr/lib/sysusers.d/
}
