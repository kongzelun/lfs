# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=filesystem
pkgver=20230123
pkgrel=1
epoch=
pkgdesc="Root file system"
arch=('any')
license=('KZL')
depends=(iana-etc)
backup=(etc/crypttab
        etc/fstab
        etc/group
        etc/gshadow
        etc/host.conf
        etc/hosts
        etc/issue
        etc/ld.so.conf
        etc/motd
        etc/nsswitch.conf
        etc/passwd
        etc/profile
        etc/resolv.conf
        etc/shadow
        etc/shells
        etc/subgid
        etc/subuid)
source=(crypttab
        fstab
        group
        gshadow
        host.conf
        hosts
        issue
        ld.so.conf
        motd
        nsswitch.conf
        passwd
        profile
        resolv.conf
        shadow
        shells
        subgid
        subuid)
sha256sums=('a6f0ad7acc6054199fbab680a53769a936a4518a2ee751404839b77fb2a4be79'
            '63ea61478aeaab0640ac76d915e94ddf3a0c8a661ee1188c8365536141c7afc2'
            '244f0718ee2a9d6862ae59d6c18c1dd1568651eada91a704574fa527fbac2b3a'
            '90d879374f77bac47f132164c1e7fc4892e994ff1d1ac376efa0c1c26ea37273'
            '4d7b647169063dfedbff5e1e22cee77bd1a4183dbcfd5e802e68939da4bbf733'
            'd9cd8a77d9e0aa5e90d7f4ed74c8745c17b525e720e28e4c44364150003c35f9'
            'd9c7dc6be96564223d7720103136e4ddc13ad7ad5f9cd95dc2718fee0216342f'
            'dad04a370e488aa85fb0a813a5c83cf6fd981ce01883fc59685447b092de84b5'
            '404c3f2e1d3d4097cedd08a4484119cb37d9adfc0ead49bdf5960e227e9159de'
            'e40e27f06d9f1d5acb727a3119a9b46bef469e09a7999220d81fabcffa8c10f4'
            '5e06477834f51abf42ea4e8dc199632afc6afbfd8c44354685a271e9a48d2c0a'
            'c82c8f0c7564c1234a2ccc11d359545b2da69643ec752865e63e0cd0d11beb30'
            '5557d8e601b17a80d1ea7de78a9869be69637cb6a02fbfe334e22fdf64e61d4c'
            '9db3df83b448ea95eadb8cec9969fea989695693c1501840fe6d5b68bbf60402'
            'c390b31fffc4a2b5d78ae8c89f5317aadef1f71baac09cfb467b675db1406d61'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855')

pkgver() {
  date +%Y%m%d
}

package() {
  cd $pkgdir

  # root filesystem
  install -m 755 -d {boot,dev,etc,home,mnt,usr,var,opt,srv,run}
  install -m 0750 -d root
  install -m 555 -d {proc,sys}
  install -m 1777 -d tmp

  # /etc
  install -m 755 -d etc/{skel,profile.d} usr/share/factory/etc
  for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf passwd profile resolv.conf shells subgid subuid; do
    install -D -m 644 "$srcdir/$f" etc/
    install -D -m 644 "$srcdir/$f" usr/share/factory/etc/
  done
  ln -s ../proc/self/mounts etc/mtab
  for f in crypttab gshadow shadow; do
    install -D -m 600 "$srcdir/$f" etc/
    install -D -m 600 "$srcdir/$f" usr/share/factory/etc/
  done

  # /usr
  install -m 755 -d usr/{bin,include,lib,libexec,share,src}
  install -m 755 -d usr/share/man/man{1..8}
  ln -s usr/lib lib
  ln -s usr/lib lib64
  ln -s lib usr/lib64
  ln -s usr/bin bin
  ln -s usr/bin sbin
  ln -s bin usr/sbin

  # /usr/local
  install -m 755 -d usr/local/{bin,etc,include,lib,libexec,share,src}
  ln -s bin usr/local/sbin

  # var
  install -m 755 -d var/{cache,crash,lib,local,log,mail,opt,spool}
  install -m 1777 -d var/{crash,tmp}
  ln -s ../mail var/spool/mail
}
