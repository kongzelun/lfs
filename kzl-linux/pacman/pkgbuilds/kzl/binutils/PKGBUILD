# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=binutils
pkgver=2.43.1.g176
pkgrel=9
epoch=
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="https://www.gnu.org/software/binutils/"
license=('KZL')
depends=(
  # jansson
  zlib
  zstd
)
checkdepends=(
  bc
  # debuginfod
  # dejagnu
)
# optdepends=("debuginfod: for debuginfod server/client functionality")
options=(staticlibs)
_branch_ver=${pkgver#*.}
_branch_ver=${_branch_ver%%.*}
_branch_ver="${pkgver%%.*}_${_branch_ver}"
source=(git+https://sourceware.org/git/binutils-gdb.git#branch=$pkgname-$_branch_ver-branch)
sha256sums=('SKIP')

_common_options=(
  "--prefix=/usr"
  "--sysconfdir=/etc"
  "--build=$CHOST"
  "--host=$CHOST"
  "--target=$CHOST"
  "--enable-gold=yes"
  "--enable-ld=default"
  "--enable-lto"
  "--with-system-zlib"
  "--with-zstd=yes"
  # subpackage common
  "--enable-plugins"
  "--enable-targets=x86_64-pep"
  "--with-pic"
  "--with-lib-path=/usr/lib:/usr/local/lib"
)

_binutils_options=(
  "--enable-deterministic-archives"
  "--enable-colored-disassembly"
  "--with-debuginfod=no"
)

_gold_ld_options=(
  "--enable-relro"
  "--enable-jansson=no"
)

_gold_options=(
  "--enable-threads=yes"
)

_ld_options=(
  "--enable-new-dtags"
  "--enable-default-execstack=no"
)

_libiberty_options=(
  "--enable-install-libiberty"
  "--enable-cet"
)

pkgver() {
  cd binutils-gdb

  git describe --tags | sed 's/^binutils-//;s/-branch//;s/-g.*//;s/_/./g;s/-/.g/g'
}

prepare() {
  cd binutils-gdb

  # remove items that are not in tarball
  rm -r gdb gdbserver gdbsupport contrib gnulib libbacktrace libdecnumber readline sim
  rm COPYING.LIBGLOSS COPYING.NEWLIB djunpack.bat
  # remove unnecessary items
  rm -r zlib

  mkdir -p build

  sed -i '/^development=/s/true/false/' bfd/development.sh
}

build() {
  cd binutils-gdb/build

  "../configure" \
    "${_common_options[@]}" \
    "${_binutils_options[@]}" \
    "${_gold_ld_options[@]}" \
    "${_gold_options[@]}" \
    "${_ld_options[@]}" \
    "${_libiberty_options[@]}"

  make tooldir="/usr"
}

check() {
  cd binutils-gdb/build

  make check
}

package() {
  cd binutils-gdb/build

  make DESTDIR="$pkgdir" tooldir="/usr" install

  # install PIC version of libiberty
  install -Dm644 libiberty/libiberty.a "$pkgdir"/usr/lib/

  # remove unnecessary man files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,windmc,windres}*
}
