# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=binutils
pkgver=2.42.82
pkgrel=1
epoch=
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="https://www.gnu.org/software/binutils/"
license=('KZL')
depends=(jansson zlib zstd)
# checkdepends=(bc debuginfod dejagnu)
checkdepends=(bc)
# optdepends=("debuginfod: for debuginfod server/client functionality")
options=(staticlibs)
source=(git+https://sourceware.org/git/binutils-gdb.git#branch=$pkgname-${pkgver/./_}-branch)
sha256sums=('SKIP')

_common_options=(
  --prefix=/usr
  --sysconfdir=/etc
  --libdir=/usr/lib
  --build=x86_64-linux-gnu
  --host=x86_64-linux-gnu
  --target=x86_64-linux-gnu
  --enable-gold=yes
  --enable-ld=default
  --enable-pgo-build=lto
  --enable-lto
  --with-system-zlib
  --with-zstd=yes
  # subpackage common
  --enable-shared
  --enable-plugins
  --enable-relro
  --enable-targets=x86_64-pep
  --enable-threads=yes
  --enable-jansson=yes
  --enable-cet
  --with-pic
  --with-lib-path=/usr/lib:/usr/local/lib
)

_binutils_options=(
  --enable-deterministic-archives
  --enable-colored-disassembly
  --with-debuginfod=no
)

_ld_options=(
  --enable-new-dtags
  --enable-default-execstack=no
)

_libiberty_options=(
  --enable-install-libiberty
)

_gdb_options=(
  --disable-gdb
  --disable-gdbserver
  --disable-gdbsupport
  --disable-libdecnumber
  --disable-readline
  --disable-sim
)

pkgver() {
  cd binutils-gdb

  git describe --tags | sed 's/^binutils-//;s/-branch//;s/-g.*//;s/-/./g;s/_/./g'
}

prepare() {
  cd binutils-gdb

  mkdir -p build

  sed -i '/^development=/s/true/false/' bfd/development.sh
}

build() {
  cd binutils-gdb/build

  "../configure" \
    "${_common_options[@]}" \
    "${_binutils_options[@]}" \
    "${_ld_options[@]}" \
    "${_libiberty_options[@]}" \
    "${_gdb_options[@]}"

  make tooldir="/usr"
}

check() {
  cd binutils-gdb/build

  make check
}

package() {
  cd binutils-gdb/build

  make DESTDIR="$pkgdir" tooldir="/usr" install

  # install PIC version of libiberty
  install -Dm644 libiberty/pic/libiberty.a "$pkgdir"/usr/lib/

  # remove unnecessary man files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,windmc,windres}*
}
