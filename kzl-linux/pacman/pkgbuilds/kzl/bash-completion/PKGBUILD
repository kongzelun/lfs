# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=bash-completion
pkgver=2.11.814
pkgrel=1
epoch=
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="https://github.com/scop/bash-completion"
license=('KZL')
depends=(bash)
options=(!emptydirs !makeflags)
source=(git+https://github.com/scop/bash-completion.git)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/-g.*//' -e 's/-/./'
}

prepare() {
  cd $pkgname

  autoreconf -vfi
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
  rm "$pkgdir"/etc/profile.d/bash_completion.sh
  rm "$pkgdir"/usr/share/bash-completion/completions/makepkg
}
