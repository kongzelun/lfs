# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rust
pkgver=1.71.0
pkgrel=4
epoch=
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('KZL')
depends=(curl llvm-project)
makedepends=(libffi)
optdepends=("gdb: rust-gdb script")
source=(https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz{,.asc})
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE')
sha256sums=('a667e4abdc5588ebfea35c381e319d840ffbf8d2dbfb79771730573642034c96'
            'SKIP')

prepare() {
  cd rustc-$pkgver-src

  _options=(
    --disable-debug
    --disable-docs
    --disable-compiler-docs
    ################
    # stage2
    ################
    --enable-local-rust
    --local-rust-root=/usr
    ################
    --disable-llvm-static-stdcpp
    --enable-llvm-link-shared
    --disable-rpath
    --enable-option-checking
    --enable-vendor
    --disable-sanitizers
    --prefix=/usr
    --llvm-root=/usr
    --llvm-config=/usr/bin/llvm-config
    --llvm-filecheck=/usr/bin/FileCheck
    --python=/usr/bin/python3
    --release-channel=stable
    --disable-manage-submodules
    --enable-full-bootstrap
    --enable-extended
    --tools=cargo,rls,clippy,rustfmt,analysis,src
    --build=x86_64-unknown-linux-gnu
    --host=x86_64-unknown-linux-gnu
    --target=x86_64-unknown-linux-gnu
    --set target.x86_64-unknown-linux-gnu.linker=clang
    --set target.x86_64-unknown-linux-gnu.cc=clang
    --set target.x86_64-unknown-linux-gnu.cxx=clang++
    --set target.x86_64-unknown-linux-gnu.ar=llvm-ar
    --set target.x86_64-unknown-linux-gnu.ranlib=llvm-ranlib
    # --set target.x86_64-unknown-linux-gnu.linker=gcc
    # --set target.x86_64-unknown-linux-gnu.cc=gcc
    # --set target.x86_64-unknown-linux-gnu.cxx=g++
    # --set target.x86_64-unknown-linux-gnu.ar=ar
    # --set target.x86_64-unknown-linux-gnu.ranlib=ranlib
  )

  ./configure \
    ${_options[@]} \
    --release-description="KZL $(date -u '+%Y/%m/%d %T %z')"

}

build() {
  cd rustc-$pkgver-src

  RUST_BACKTRACE=full ./x.py -j "$(nproc)" build
}

check() {
  cd rustc-$pkgver-src

  ./x.py test
}

package() {
  cd rustc-$pkgver-src

  DESTDIR="$pkgdir" ./x.py -j "$(nproc)" install

  rm "$pkgdir"/usr/lib/rustlib/{components,install.log,manifest-*,rust-installer-version,uninstall.sh}

  install -dm755 "$pkgdir"/usr/share/bash-completion/completions/
  mv "$pkgdir"/usr/src/etc/bash_completion.d "$pkgdir"/usr/share/bash-completion/completions/
  rmdir "$pkgdir"/usr/src/etc
  rmdir "$pkgdir"/usr/src
}
