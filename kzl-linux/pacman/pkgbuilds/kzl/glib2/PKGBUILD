# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=glib2
pkgver=2.72.1
pkgrel=1
epoch=1
pkgdesc="Low level core library"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GLib"
license=('KZL')
# depends=(libffi libsysprof-capture pcre)
depends=(libffi pcre)
makedepends=(dbus elfutils shared-mime-info)
optdepends=("elfutils: gresource inspection tool")
options=(!strip staticlibs)
source=(git+https://gitlab.gnome.org/GNOME/glib.git#tag=$pkgver?signed
        gio-querymodules.hook
        glib-compile-schemas.hook)
sha256sums=('SKIP'
            '2a6ed172633430753af2d4eb2bc266c2e12713c0a68d8e12e885ea0595a86460'
            '018c62a1087461cb1f5dd2a879667d7218e4990c1b4c322e584dc3ef311342ca')
validpgpkeys=('923B7025EE03C1C59F42684CF0942E894B2EAFA0')

# TODO: sysprof

build() {
  cd glib

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    --default-library both \
    -D b_lto=true \
    -D b_pie=true \
    -D gtk_doc=false \
    -D man=true \
    -D selinux=disabled \
    -D sysprof=disabled \
    build

  meson compile -C build
}

check() {
  cd glib

  meson test -C build --no-suite flaky --print-errorlogs
}

package() {
  cd glib

  meson install -C build --destdir="$pkgdir/"

  sed -e 's/, sysprof-capture-4//' -i "$pkgdir"/usr/lib/pkgconfig/*.pc

  install -Dm644 "$srcdir"/*.hook -t "$pkgdir"/usr/share/libalpm/hooks

  export PYTHONHASHSEED=0
  python -m compileall -d /usr/share/glib-2.0/codegen "$pkgdir/usr/share/glib-2.0/codegen"
  python -O -m compileall -d /usr/share/glib-2.0/codegen "$pkgdir/usr/share/glib-2.0/codegen"
}
