# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=autoconf
pkgver=2.71
pkgrel=1
epoch=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
url="https://www.gnu.org/software/autoconf"
license=('KZL')
depends=(diffutils)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f14c83cfebcc9427f2c3cea7258bd90df972d92eb26752da4ddad81c87a0faa4'
            'SKIP')
validpgpkeys=('82F854F3CE73174B8B63174091FCC32B6769AA64')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

	make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  rm -f "$pkgdir"/usr/share/info/standards.info
}
