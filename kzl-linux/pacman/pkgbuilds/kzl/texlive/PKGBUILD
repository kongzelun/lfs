pkgname=texlive
pkgver=66539
_pkgyear=2023
pkgrel=11
epoch=
pkgdesc="TeX Live"
arch=('x86_64')
url="https://www.tug.org/historic/"
license=('KZL')
depends=(cairo fontconfig freetype2 gd gmp graphite harfbuzz icu libpaper libpng mpfr pixman zziplib)
install=$pkgname.install
source=(git+https://github.com/TeX-Live/texlive-source.git#tag=svn$pkgver
        git+https://github.com/TeX-Live/installer.git
        # http://tug.ctan.org/systems/texlive/tlnet/tlpkg/texlive.tlpdb
        ################################################################################
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-$pkgver-source.tar.xz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/install-tl-unx.tar.gz
        http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-20230311-tlpdb-full.tar.gz
        http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-20230313-texmf.tar.xz
        09-texlive-fonts.conf
        $pkgname.install)
noextract=(texlive-20230313-texmf.tar.xz)
sha256sums=('SKIP'
            'SKIP'
            'bf3360ebe897dac8424425ba3a87dbd41433aa92924c04be1e47285f264f6c8a'
            '4c4dc77a025acaad90fb6140db2802cdb7ca7a9a2332b5e3d66aa77c43a81253'
            'fe01050544263a517c9a682942edc1cbb150986c34eab3b6755143f072838e93'
            'fd12d52d23a1c6f5dfb8d6818014c6de12ee71a106c486b373fa9073bbb41cf1')

prepare() {
  cd $pkgname-source

  sed -e '/AC_SEARCH_LIBS/a KPSE_KPATHSEA_FLAGS' -i texk/bibtex-x/configure.ac
  (cd texk/bibtex-x && autoreconf)
  sed -e 's/SELFAUTOPARENT/TEXMFROOT/' -i texk/tex4htk/t4ht.c

  mkdir build
}

build() {
  cd $pkgname-source/build

  ../configure \
    --prefix=/opt/TeXLive \
    --datarootdir=/opt/TeXLive \
    --disable-native-texlive-build \
    --disable-multiplatform \
    --disable-t1utils \
    --disable-dump-share \
    --disable-aleph \
    --enable-xetex \
    --disable-bibtexu \
    --disable-psutils \
    --enable-shared \
    --disable-static \
    --with-clisp-runtime=default \
    --with-banner-add="/KZL $(date +%Y.%m.%d)" \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-graphite2 \
    --with-system-zziplib \
    --with-system-mpfr \
    --with-system-gmp \
    --with-system-cairo \
    --with-system-pixman \
    --with-system-gd \
    --with-system-freetype2 \
    --with-system-libpng \
    --with-system-libpaper \
    --with-system-zlib \
    --without-x

  make
}

package() {
  cd $pkgname-source/build

  make DESTDIR="$pkgdir" install-strip
  make DESTDIR="$pkgdir" texlinks

  ### enable tlmgr
  mkdir -p "$pkgdir"/opt/TeXLive/tlpkg/{backups,installer,tlpobj}
  cp -r "$srcdir"/installer/tlpkg/{TeXLive,gpg,translations} "$pkgdir"/opt/TeXLive/tlpkg/
  cp -r "$srcdir"/installer/tlpkg/installer/{config.guess,ctan-mirrors.pl} "$pkgdir"/opt/TeXLive/tlpkg/installer/
  cp "$srcdir"/texlive.tlpdb "$pkgdir"/opt/TeXLive/tlpkg/

  ### texmf-dist
  tar -xf "$srcdir"/texlive-20230313-texmf.tar.xz -C "$pkgdir"/opt/TeXLive --skip-old-files --strip-components=1
  cp -r "$srcdir"/installer/texmf-dist "$pkgdir"/opt/TeXLive

  sed -e 's/SELFAUTOPARENT/SELFAUTODIR/g' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/scripts/texlive/tlmgr.pl
  sed -e 's/% original/% TeXLive\/KZL/' \
      -e "s/texlive$_pkgyear/texlive/g" \
      -e '/^TEXMFROOT/s/SELFAUTOPARENT/SELFAUTODIR/' \
      -e '/^TEXMFLOCAL/s/SELFAUTOGRANDPARENT/SELFAUTODIR/' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/web2c/texmf.cnf
  sed -e '/TEXMFSYSVAR     = "selfautoparent:texmf-var"/s/selfautoparent/selfautodir/' \
      -e "/home:.texlive$_pkgyear/s/texlive$_pkgyear/texlive/" \
      -e '/TEXMFDIST       = "selfautoparent:texmf-dist"/s/selfautoparent/selfautodir/' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/web2c/texmfcnf.lua

  echo "export PATH=\$PATH:/opt/TeXLive/bin" | install -Dm644 /dev/stdin "$pkgdir"/etc/profile.d/texlive.sh

  # texlive fonts
  install -Dm644 "$srcdir"/09-texlive-fonts.conf -t "$pkgdir"/usr/share/fonts/conf.avail/

  # cleanup
  rm -rf "$pkgdir"/opt/TeXLive/texmf-dist/scripts/context/stubs/mswin
}
