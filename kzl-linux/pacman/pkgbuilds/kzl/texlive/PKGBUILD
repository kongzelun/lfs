pkgname=texlive
pkgver=20220321
pkgrel=2
epoch=1
pkgdesc="TeX Live"
arch=('x86_64')
url="https://www.tug.org/historic/"
license=('KZL')
depends=(cairo fontconfig freetype2 gd gmp graphite harfbuzz icu libpaper libpng mpfr pixman zlib zziplib)
provides=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
conflicts=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
replaces=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
options=(!strip)
install=$pkgname.install
source=(git+https://github.com/TeX-Live/texlive-source.git#commit=f7b6aab729bd6e8723b4b13ef0ce910437852d03
        git+https://github.com/TeX-Live/installer.git
        http://tug.ctan.org/systems/texlive/tlnet/tlpkg/texlive.tlpdb
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/${pkgver:0:4}/texlive-$pkgver-source.tar.xz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/${pkgver:0:4}/texlive-${pkgver:0:6}25-tlpdb-full.tar.gz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/${pkgver:0:4}/install-tl-unx.tar.gz
        http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/${pkgver:0:4}/texlive-$pkgver-texmf.tar.xz
        09-texlive-fonts.conf
        $pkgname.install)
noextract=(texlive-$pkgver-texmf.tar.xz)
sha256sums=('SKIP'
            'SKIP'
            '52b295aa4e7471bf69dc14ab86beb9d6de212187d99e10c4dd144c81ad0e7b45'
            '372b2b07b1f7d1dd12766cfc7f6656e22c34a5a20d03c1fe80510129361a3f16'
            'fe01050544263a517c9a682942edc1cbb150986c34eab3b6755143f072838e93'
            'af6b83c35bcc7a07397f36e16e70ae203c3a7520225dfbeee10f45fc8adacd3f')

prepare() {
  cd $pkgname-source

  sed -e '/AC_SEARCH_LIBS/a KPSE_KPATHSEA_FLAGS' -i texk/bibtex-x/configure.ac
  (cd texk/bibtex-x && autoreconf)
  sed -e 's/SELFAUTOPARENT/TEXMFROOT/' -i texk/tex4htk/t4ht.c

  mkdir build
}

build() {
  cd $pkgname-source/build

  ../configure \
    --prefix=/opt/TeXLive \
    --datarootdir=/opt/TeXLive \
    --disable-native-texlive-build \
    --disable-multiplatform \
    --disable-t1utils \
    --disable-dump-share \
    --disable-aleph \
    --enable-xetex \
    --disable-bibtexu \
    --disable-psutils \
    --enable-shared \
    --disable-static \
    --with-clisp-runtime=default \
    --with-banner-add="/KZL $(date -Ru)" \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-graphite2 \
    --with-system-zziplib \
    --with-system-mpfr \
    --with-system-gmp \
    --with-system-cairo \
    --with-system-pixman \
    --with-system-gd \
    --with-system-freetype2 \
    --with-system-libpng \
    --with-system-libpaper \
    --with-system-zlib \
    --without-x

  make
}

package() {
  cd $pkgname-source/build

  make DESTDIR="$pkgdir/" install-strip
  make DESTDIR="$pkgdir/" texlinks

  ### enable tlmgr
  mkdir -p "$pkgdir"/opt/TeXLive/tlpkg/{backups,installer,tlpobj}
  cp -r "$srcdir"/installer/tlpkg/{TeXLive,gpg,translations} "$pkgdir"/opt/TeXLive/tlpkg/
  cp -r "$srcdir"/installer/tlpkg/installer/{config.guess,ctan-mirrors.pl} "$pkgdir"/opt/TeXLive/tlpkg/installer/
  cp "$srcdir"/texlive.tlpdb "$pkgdir"/opt/TeXLive/tlpkg/

  ### texmf-dist
  tar -xf "$srcdir"/$pkgname-$pkgver-texmf.tar.xz -C "$pkgdir"/opt/TeXLive --skip-old-files --strip-components=1
  cp -r "$srcdir"/installer/texmf-dist "$pkgdir"/opt/TeXLive

  sed -e 's/SELFAUTOPARENT/SELFAUTODIR/g' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/scripts/texlive/tlmgr.pl
  sed -e 's/% original/% TeXLive\/KZL/' \
      -e "s/texlive${pkgver:0:4}/texlive/g" \
      -e '/^TEXMFROOT/s/SELFAUTOPARENT/SELFAUTODIR/' \
      -e '/^TEXMFLOCAL/s/SELFAUTOGRANDPARENT/SELFAUTODIR/' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/web2c/texmf.cnf

  echo "export PATH=\$PATH:/opt/TeXLive/bin" | install -Dm644 /dev/stdin "$pkgdir"/etc/profile.d/texlive.sh

  # texlive fonts
  install -dm755 "$pkgdir"/etc/fonts/conf.avail/
  install -Dm644 "$srcdir"/09-texlive-fonts.conf "$pkgdir"/etc/fonts/conf.avail/

  # cleanup
  rm -rf "$pkgdir"/opt/TeXLive/texmf-dist/scripts/context/stubs/mswin
}
