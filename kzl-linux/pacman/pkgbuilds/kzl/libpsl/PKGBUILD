# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libpsl
pkgver=0.21.2
pkgrel=8
epoch=
pkgdesc="C library to handle the Public Suffix List"
arch=('x86_64')
url="https://github.com/rockdaboot/libpsl"
depends=(libidn2)
makedepends=(libxslt)
source=(git+$url.git#tag=$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  git submodule update --init
}

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --auto-features enabled \
    --optimization 2 \
    -D builtin=true \
    -D docs=false \
    -D runtime=libidn2 \
    build

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build
}

package() {
  cd $pkgname

  meson install -C build --destdir="$pkgdir"
}
