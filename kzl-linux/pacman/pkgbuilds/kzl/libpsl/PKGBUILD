# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libpsl
pkgver=0.21.1
pkgrel=1
epoch=1
pkgdesc="Public Suffix List library"
arch=('x86_64')
url="https://github.com/rockdaboot/libpsl"
depends=(libidn2)
makedepends=(libxslt publicsuffix-list)
source=($url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ac6ce1e1fbd4d0254c4ddb9d37f1fa99dec83619c1253328155206b896210d4c')

prepare() {
  cd $pkgname-$pkgver

  rm -rf list
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-gtk-doc \
    --disable-man \
    --enable-runtime=libidn2 \
    --enable-builtin=libidn2 \
    --disable-dependency-tracking \
    --with-psl-file=/usr/share/publicsuffix/public_suffix_list.dat \
    --with-psl-testfile=/usr/share/publicsuffix/test_psl.txt

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
