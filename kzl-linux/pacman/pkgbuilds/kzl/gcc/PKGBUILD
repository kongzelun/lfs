# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=gcc
pkgver=12.2.0
pkgrel=1
epoch=
pkgdesc="The GNU Compiler Collection"
arch=('x86_64')
url="https://gcc.gnu.org"
license=('KZL')
depends=(gmp isl mpc mpfr)
makedepends=(binutils git)
options=(!emptydirs)
source=(git+git://gcc.gnu.org/git/gcc.git#tag=releases/$pkgname-$pkgver)
sha256sums=('SKIP')

prepare() {
  cd gcc

  mkdir -p build

  sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64
}

build() {
  cd gcc/build

  ../configure \
    --with-pkgversion="KZL $pkgver-$pkgrel `date -u '+%Y/%m/%d %T %z'`" \
    --prefix=/usr \
    --build=x86_64-linux-gnu \
    --host=x86_64-linux-gnu \
    --target=x86_64-linux-gnu \
    --bindir=/usr/bin \
    --libdir=/usr/lib \
    --libexecdir=/usr/libexec \
    --with-slibdir=/usr/lib \
    --enable-shared \
    --disable-multilib \
    --enable-threads=posix \
    --enable-__cxa_atexit \
    --enable-gnu-indirect-function \
    --enable-languages=c,c++,fortran,lto \
    --disable-libssp \
    --disable-libquadmath \
    --disable-libquadmath-support \
    --enable-default-pie \
    --enable-default-ssp \
    --enable-checking=release \
    --with-isl \
    --enable-linker-build-id \
    --with-linker-hash-style=gnu \
    --enable-gnu-unique-object \
    --enable-lto \
    --enable-cet=auto\
    --with-zstd \
    --with-target-system-zlib \
    --enable-plugin \
    --disable-install-libiberty \
    --enable-clocale=gnu \
    --enable-libstdcxx-time=yes \
    --enable-libstdcxx-debug \
    --disable-libstdcxx-pch \
    --with-default-libstdcxx-abi=new

  make -O
}

check() {
  cd gcc/build

  make -O -k check || warning "Check failed."
}

package() {
  cd gcc/build

  make DESTDIR="$pkgdir/" install

  ln -s gcc "$pkgdir"/usr/bin/cc
}
