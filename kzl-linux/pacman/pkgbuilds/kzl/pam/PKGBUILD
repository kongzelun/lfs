# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pam
pkgver=1.5.3
pkgrel=5
epoch=
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
url="https://github.com/linux-pam/linux-pam"
license=('KZL')
depends=(
  audit
  glibc
  libtirpc
  libxcrypt
  pam-config
)
source=(git+https://github.com/linux-pam/linux-pam.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd linux-pam

  ./autogen.sh
}

build() {
  cd linux-pam

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/sbin \
    --libdir=/usr/lib \
    --disable-doc \
    --disable-prelude \
    --enable-audit \
    --enable-db=no \
    --disable-nis \
    --disable-selinux \
    --disable-openssl

  make
}

check() {
  cd linux-pam

  make check
}

package() {
  cd linux-pam

  make DESTDIR="$pkgdir" install
}
