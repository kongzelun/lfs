# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=bash
pkgver=5.2.15
pkgrel=1
epoch=
pkgdesc="The GNU Bourne-Again SHell"
arch=('x86_64')
url="https://savannah.gnu.org/projects/bash/"
license=('KZL')
depends=(glibc ncurses readline)
optdepends=("bash-completion: for tab completion")
provides=(sh)
source=(git+https://git.savannah.gnu.org/git/bash.git#branch=master
        dot.bashrc
        dot.bash_logout
        dot.bash_profile
        system.bashrc
        system.bash_logout)
sha256sums=('SKIP'
            '3e22bf86ae6708df7a6bceb88c67a00118275f9c0b5268f453dd388af7c43b53'
            '4330edf340394d0dae50afb04ac2a621f106fe67fb634ec81c4bfb98be2a1eb5'
            'e149407c2bee17779caec70a7edd3d0000d172e7e4347429b80cb4d55bcec9c2'
            '5fdc20c44bc9058f728d11111327f4dbb5598fec4d948dd5265211598667f9f0'
            '025bccfb374a3edce0ff8154d990689f30976b78f7a932dc9a6fcef81821811e')

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/bash-//' -e 's/-g.*//' -e 's/-/./'
}

build() {
  cd $pkgname

  # config-top.h
  _bashconfig=(
    -DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/local/sbin:/usr/bin\"\'
    -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
    -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
    -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
    -DNON_INTERACTIVE_LOGIN_SHELLS
  )

  CFLAGS+=" ${_bashconfig[@]}"

  ./configure \
    --prefix=/usr \
    --with-curses \
    --enable-readline \
    --without-bash-malloc \
    --with-installed-readline

  make -O
}

check() {
  cd $pkgname

  make -O check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install

  ln -s bash "$pkgdir"/usr/bin/sh

  # system-wide configuration files
  install -Dm644 "$srcdir"/system.bashrc "$pkgdir"/etc/bash.bashrc
  install -Dm644 "$srcdir"/system.bash_logout "$pkgdir"/etc/bash.bash_logout

  # user configuration file skeletons
  install -dm755 "$pkgdir"/etc/skel/
  install -Dm644 "$srcdir"/dot.bashrc "$pkgdir"/etc/skel/.bashrc
  install -Dm644 "$srcdir"/dot.bash_profile "$pkgdir"/etc/skel/.bash_profile
  install -Dm644 "$srcdir"/dot.bash_logout "$pkgdir"/etc/skel/.bash_logout
}
