# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=m4
pkgver=1.4.19.22
pkgrel=3
epoch=
pkgdesc="An implementation of the traditional Unix macro processor"
arch=('x86_64')
url="https://www.gnu.org/software/m4/"
license=('KZL')
depends=(libsigsegv)
source=(git+https://git.savannah.gnu.org/git/m4.git#branch=branch-${pkgver:0:3}
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/v//;s/-g.*//;s/-/./g'
}

prepare() {
	cd $pkgname

  ./bootstrap --gnulib-srcdir="$srcdir/gnulib"
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-nls

  make
}

check() {
  cd $pkgname

	make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
