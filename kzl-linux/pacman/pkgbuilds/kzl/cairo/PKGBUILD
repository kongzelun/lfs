# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=cairo
pkgver=1.17.6
pkgrel=1
epoch=1
pkgdesc="2D graphics library with support for multiple output devices"
arch=('x86_64')
url="https://cairographics.org/"
license=('KZL')
depends=(fontconfig freetype2 glib2 libpng lzo pixman zlib)
makedepends=(valgrind)
source=(git+https://gitlab.freedesktop.org/cairo/cairo.git#tag=$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D fontconfig=enabled \
    -D freetype=enabled \
    -D glib=enabled \
    -D gtk_doc=false \
    -D png=enabled \
    -D spectre=disabled \
    -D symbol-lookup=disabled \
    -D tee=enabled \
    -D tests=disabled \
    -D xcb=disabled \
    -D xlib=disabled \
    -D xlib-xcb=disabled \
    -D zlib=enabled \
    build

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" meson install -C build
}
