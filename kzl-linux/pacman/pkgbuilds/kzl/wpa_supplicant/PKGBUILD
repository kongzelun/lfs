# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=wpa_supplicant
pkgver=2.10
pkgrel=1
epoch=
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=('x86_64')
url="https://w1.fi/wpa_supplicant/"
license=('KZL')
depends=(libnl openssl)
source=(https://w1.fi/releases/$pkgname-$pkgver.tar.gz{,.asc}
  defconfig)
sha256sums=('20df7ae5154b3830355f8ab4269123a87affdea59fe74fe9292a91d0d7e17b2f'
            'SKIP'
            '5cfc8760d15c30c502e90ebfb447b0d64063ec943c3c721822871f8064f8d5b8')
validpgpkeys=('EC4AA0A991A5F2464582D52D2B6EF432EFC895FA')

prepare() {
  cd $pkgname-$pkgver/$pkgname

  # wpa_supplicant/defconfig
  cp "$srcdir"/defconfig .config
}

build() {
  cd $pkgname-$pkgver/$pkgname

  make LIBDIR=/usr/lib BINDIR=/usr/bin
  make LIBDIR=/usr/lib BINDIR=/usr/bin eapol_test
}

package() {
  cd $pkgname-$pkgver/$pkgname

  make LIBDIR=/usr/lib BINDIR=/usr/bin DESTDIR="$pkgdir/" install
  install -Dm755 eapol_test "$pkgdir"/usr/bin/eapol_test

  install -dm755 "$pkgdir"/etc/wpa_supplicant/
  install -Dm644 wpa_supplicant.conf "$pkgdir"/usr/share/doc/wpa_supplicant/wpa_supplicant.conf

  install -dm755 "$pkgdir"/usr/share/dbus-1/{system.d,system-services}
  install -Dm644 dbus/fi.w1.wpa_supplicant1.service "$pkgdir"/usr/share/dbus-1/system-services/

  install -Dm644 dbus/dbus-wpa_supplicant.conf "$pkgdir"/usr/share/dbus-1/system.d/wpa_supplicant.conf

  install -dm755 "$pkgdir"/usr/share/man/man{5,8}/
  install -Dm644 doc/docbook/*.5 "$pkgdir"/usr/share/man/man5/
  install -Dm644 doc/docbook/*.8 "$pkgdir"/usr/share/man/man8/
  rm -f "$pkgdir"/usr/share/man/man8/wpa_{priv,gui}.8

  install -dm755 "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 systemd/*.service "$pkgdir"/usr/lib/systemd/system/
}
