# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=glibc
pkgver=2.36
pkgrel=1
epoch=
arch=('x86_64')
url="https://www.gnu.org/software/libc"
license=('KZL')
depends=(filesystem linux-api-headers tzdata)
makedepends=(git)
options=(staticlibs !lto)
source=(git+https://sourceware.org/git/glibc.git#branch=release/$pkgver/master
        locale-gen
        locale.gen)
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
install=$pkgname.install
sha256sums=('SKIP'
            '436293c91051efcf712a71ee27e758387ddc82996ad23079d5ab30c0947963b9'
            '949d9660b9ecef93776aebde70ffff161fa58c245636c21d80f3beaa919391ca')

prepare() {
  cd $pkgname

  mkdir -p build

  echo 'prefix = /usr
build-programs = yes
exec_prefix = $(prefix)
libdir = $(exec_prefix)/lib
slibdir = $(exec_prefix)/lib
rtlddir = $(slibdir)
includedir = $(prefix)/include
bindir = $(exec_prefix)/bin
libexecdir = $(exec_prefix)/libexec
rootsbindir = $(exec_prefix)/bin
sbindir = $(exec_prefix)/bin
sysconfdir = /etc' > build/configparms
}

build() {
  cd $pkgname/build

  CFLAGS="${CFLAGS/-Wp,-D_FORTIFY_SOURCE=2/}"

  ../configure \
    --prefix=/usr \
    --with-headers=/usr/include \
    --enable-kernel=5.15 \
    --with-nonshared-cflags="-Wp,-D_FORTIFY_SOURCE=2" \
    --enable-cet \
    --disable-profile \
    --disable-timezone-tools \
    --enable-stack-protector=strong \
    --enable-bind-now \
    --disable-werror \
    --disable-crypt \
    --with-pkgversion="KZL $pkgver-$pkgrel"

  make -O
}

check() {
  cd $pkgname/build

  make -O -k check || warning "Check failed."
}

package() {
  cd $pkgname/build

  make install_root="$pkgdir/" install

  rm -f "$pkgdir"/etc/ld.so.cache

  # locale
  install -m755 -d "$pkgdir"/usr/lib/locale
  install -Dm755 "$srcdir"/locale-gen "$pkgdir"/usr/bin/locale-gen
  install -Dm644 "$srcdir"/locale.gen "$pkgdir"/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's| \\||g' "$srcdir"/$pkgname/localedata/SUPPORTED > "$pkgdir"/usr/share/i18n/SUPPORTED
  sed -e 's|^|#|g' "$pkgdir"/usr/share/i18n/SUPPORTED >> "$pkgdir"/etc/locale.gen
  sed -i -e '/^#en_US\.UTF-8/s/^#//' -e '/^#zh_CN\.UTF-8/s/^#//' "$pkgdir"/etc/locale.gen

  # name service cache daemon
  install -m755 -d "$pkgdir"/usr/lib/{systemd/system,tmpfiles.d}
  install -m755 -d "$pkgdir"/var/db/nscd
  install -Dm644 "$srcdir"/$pkgname/nscd/nscd.conf "$pkgdir"/etc/nscd.conf
  install -Dm644 "$srcdir"/$pkgname/nscd/nscd.service "$pkgdir"/usr/lib/systemd/system/nscd.service
  install -Dm644 "$srcdir"/$pkgname/nscd/nscd.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/nscd.conf

  # getaddrinfo(3) configuration file
  install -Dm644 "$srcdir"/$pkgname/posix/gai.conf "$pkgdir"/etc/gai.conf
}
