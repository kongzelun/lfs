# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=glibc
pkgver=2.40.g64
pkgrel=28
epoch=
arch=('x86_64')
url="https://www.gnu.org/software/libc"
license=('KZL')
depends=(
  filesystem
  linux-api-headers
  tzdata
)
options=(staticlibs)
source=(
  git+https://sourceware.org/git/glibc.git#branch=release/${pkgver%.g*}/master
  # git+https://sourceware.org/git/glibc.git#tag=$pkgname-$pkgver
  ld.so.conf
)
backup=(
  etc/locale.gen
  etc/ld.so.conf
  etc/gai.conf
  etc/nscd.conf
)
install=$pkgname.install
sha256sums=('SKIP'
            'dad04a370e488aa85fb0a813a5c83cf6fd981ce01883fc59685447b092de84b5')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/glibc-//;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  mkdir -p build

  # Makeconfig
  echo 'prefix = /usr
build-programs = yes
exec_prefix = $(prefix)
libdir = $(exec_prefix)/lib
slibdir = $(exec_prefix)/lib
rtlddir = $(slibdir)
includedir = $(prefix)/include
datadir = $(prefix)/share
bindir = $(exec_prefix)/bin
libexecdir = $(exec_prefix)/libexec
rootsbindir = $(exec_prefix)/sbin
sbindir = $(exec_prefix)/sbin
sysconfdir = /etc' >build/configparms
}

build() {
  cd $pkgname/build

  CFLAGS="-march=haswell -O2 -pipe -fno-plt -fexceptions -fcf-protection -fstack-clash-protection -fno-omit-frame-pointer -mabm -maes -mrtm -mno-vzeroupper"

  "../configure" \
    --prefix=/usr \
    --disable-timezone-tools \
    --enable-bind-now \
    --enable-stack-protector=strong \
    --enable-kernel=6.6 \
    --disable-werror \
    --disable-nscd \
    --enable-cet \
    --enable-fortify-source=3 \
    --with-pkgversion="KZL $pkgver-$pkgrel" \
    --with-headers=/usr/include

  make
}

check() {
  cd $pkgname/build

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname/build

  make install_root="$pkgdir/" install

  # ldconfig
  install -Dm644 "$srcdir"/ld.so.conf etc/ld.so.conf

  # getaddrinfo(3) configuration file
  install -Dm644 "$srcdir"/$pkgname/posix/gai.conf "$pkgdir"/etc/gai.conf

  rm -f "$pkgdir"/etc/ld.so.cache
}
