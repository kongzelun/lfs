# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ca-certificates
pkgver=2024.01.04
pkgrel=1
epoch=
pkgdesc="CA certificates"
arch=('any')
license=('KZL')
depends=(p11-kit)
makedepends=(python)
source=(https://hg.mozilla.org/releases/mozilla-release/raw-file/default/security/nss/lib/ckfw/builtins/certdata.txt
        https://hg.mozilla.org/releases/mozilla-release/raw-file/default/security/nss/lib/ckfw/builtins/nssckbi.h
        https://src.fedoraproject.org/rpms/ca-certificates/raw/rawhide/f/certdata2pem.py
        update-ca-trust
        update-ca-trust.hook)
sha256sums=('1970dd65858925d68498d2356aea6d03f764422523c5887deca8ce3ba9e1f845'
            'b8514b69ffa34556fa10fddef604e35e4f1c1a67d567cf113d1d8277133d2d38'
            'd2a1579dae05fd16175fac27ef08b54731ecefdf414085c610179afcf62b096c'
            '92deb387e3f4d9dd4301db72c27797235997f775072687029d322808da94cd75'
            '86906969b5ebd589207dd79166dfa25561420b1b66ab496707520af3374ede64')

prepare() {
  mkdir -p certs
  ln -srf -t certs/ "$srcdir"/{certdata.txt,nssckbi.h,certdata2pem.py}
}

build() {
  cd certs

  python3 certdata2pem.py

  (
  cat <<EOF
# This is a bundle of X.509 certificates of public Certificate Authorities.
# It was generated from the Mozilla root CA list.
# These certificates and trust/distrust attributes use the file format accepted by the p11-kit-trust module.
#
# Source: nss/lib/ckfw/builtins/certdata.txt
# Source: nss/lib/ckfw/builtins/nssckbi.h
#
# Generated from:
EOF
  cat nssckbi.h | grep -w NSS_BUILTINS_LIBRARY_VERSION | awk '{print "# " $2 " " $3}';
  echo '#';
  ) > ca-bundle.trust.p11-kit

  for cert in *.tmp-p11-kit; do
    cat "$cert" >> ca-bundle.trust.p11-kit
  done
}

package() {
  cd certs

  install -d -m755 "$pkgdir"/{etc,usr/share}/$pkgname/trust-source/
  install -d -m555 "$pkgdir"/etc/$pkgname/extracted/pem-directory-hash/

  install -Dm755 "$srcdir"/update-ca-trust -t "$pkgdir"/usr/bin/
  install -Dm644 "$srcdir"/update-ca-trust.hook -t "$pkgdir"/usr/share/libalpm/hooks/

  install -Dm644 ca-bundle.trust.p11-kit "$pkgdir"/usr/share/ca-certificates/trust-source/mozilla.trust.p11-kit
}
