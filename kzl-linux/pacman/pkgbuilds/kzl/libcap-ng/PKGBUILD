# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libcap-ng
pkgver=0.8.2
pkgrel=1
epoch=1
pkgdesc="Library making programming with POSIX capabilities easier than traditional libcap"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/libcap-ng/"
provides=(libcap-ng.so)
source=($pkgname-$pkgver.tar.gz::https://github.com/stevegrubb/libcap-ng/archive/v$pkgver.tar.gz)
sha256sums=('65b86885b8d873e55c05bd49427fd370d559b26f0c2089ac9194828e6a2fe233')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --without-python

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
