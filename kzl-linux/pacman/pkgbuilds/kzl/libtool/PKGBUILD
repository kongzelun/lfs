# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libtool
pkgver=2.4.7
pkgrel=1
epoch=
pkgdesc="A generic library support script"
arch=('x86_64')
url="https://www.gnu.org/software/libtool"
license=('KZL')
makedepends=(help2man)
source=(git+https://git.savannah.gnu.org/git/libtool.git#tag=v$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git
        gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.gnulib.url "$srcdir"/gnulib
  git config --local submodule.gl-mod/bootstrap.url "$srcdir"/gnulib-bootstrap
  git submodule update

  ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    lt_cv_sys_lib_dlsearch_path_spec="/usr/lib"

  make -O
}

check() {
  cd $pkgname

  make -O check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
