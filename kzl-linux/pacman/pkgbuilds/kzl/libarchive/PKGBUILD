# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libarchive
pkgver=3.7.1
pkgrel=3
epoch=
pkgdesc="A portable, efficient C library that can read and write streaming archives in a variety of formats"
arch=('x86_64')
url="https://libarchive.org/"
license=('KZL')
depends=(acl expat lz4 nettle)
source=(git+https://github.com/libarchive/libarchive.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  ./build/autogen.sh
}


build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --enable-static=no \
    --enable-acl \
    --with-lz4 \
    --with-nettle \
    --without-xml2 \
    --with-expat

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  sed -i "s/iconv //" "$pkgdir"/usr/lib/pkgconfig/libarchive.pc
}
