# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openssh
pkgver=8.8
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.openssh.com/portable.html"
license=('KZL')
depends=(krb5 ldns libedit libxcrypt openssl)
# makedepends=(libfido2)
optdepends=("libfido2: U2F/FIDO support"
            "x11-ssh-askpass: input passphrase in X"
            "xorg-xauth: X11 forwarding")
backup=(etc/ssh/ssh_config
        etc/ssh/sshd_config
        etc/pam.d/sshd)
source=(https://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/$pkgname-${pkgver}p${pkgrel}.tar.gz{,.asc}
        sshd.conf
        sshd.pam
        sshd.service
        sshdgenkeys.service)
sha256sums=('4590890ea9bb9ace4f71ae331785a3a5823232435161960ed5fc86588f331fe9'
            'SKIP'
            '4effac1186cc62617f44385415103021f72f674f8b8e26447fc1139c670090f6'
            'd447e0d52f12b8b186e6864749d594746966ac997767e8af300190d1fbb74eaf'
            'e40f8b7c8e5e2ecf3084b3511a6c36d5b5c9f9e61f2bb13e3726c71dc7d4fbc7'
            '4031577db6416fcbaacf8a26a024ecd3939e5c10fe6a86ee3f0eea5093d533b7')
validpgpkeys=('7168B983815A5EEF59A4ADFD2A3F414E736060BA')

# TODO: libfido2 (--enable-security-key --with-security-key-builtin)

build() {
  cd $pkgname-${pkgver}p${pkgrel}

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh \
    --disable-security-key \
    --with-ldns \
    --with-libedit \
    --without-security-key-builtin \
    --with-ssl-engine \
    --with-pam \
    --with-privsep-user=nobody \
    --with-kerberos5=/usr \
    --with-xauth=/usr/bin/xauth \
    --with-md5-passwords \
    --with-default-path="/usr/local/bin:/usr/bin" \
    --with-pid-dir=/run

  make
}

package() {
  cd $pkgname-${pkgver}p${pkgrel}

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/sshdgenkeys.service "$pkgdir"/usr/lib/systemd/system/sshdgenkeys.service
  install -Dm644 "$srcdir"/sshd.service "$pkgdir"/usr/lib/systemd/system/sshd.service
  install -Dm644 "$srcdir"/sshd.conf "$pkgdir"/usr/lib/tmpfiles.d/sshd.conf
  install -Dm644 "$srcdir"/sshd.pam "$pkgdir"/etc/pam.d/sshd

  install -Dm755 contrib/findssl.sh "$pkgdir"/usr/bin/findssl.sh
  install -Dm755 contrib/ssh-copy-id "$pkgdir"/usr/bin/ssh-copy-id
  install -Dm644 contrib/ssh-copy-id.1 "$pkgdir"/usr/share/man/man1/ssh-copy-id.1

	sed -e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
      -e '/^#PrintMotd yes$/c PrintMotd no' \
      -e '/^#UsePAM no$/c UsePAM yes' \
      -i "$pkgdir"/etc/ssh/sshd_config
}
