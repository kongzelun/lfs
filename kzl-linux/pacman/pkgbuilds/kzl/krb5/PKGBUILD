# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=krb5
pkgver=1.21.1
pkgrel=3
epoch=
pkgdesc="The Kerberos network authentication protocol"
arch=('x86_64')
url="https://web.mit.edu/kerberos/"
license=('KZL')
depends=(
  e2fsprogs
  keyutils
  libedit
  lmdb
  openssl
)
backup=(
  etc/krb5.conf
  var/krb5kdc/kdc.conf
)
source=(git+https://github.com/krb5/krb5.git#tag=$pkgname-$pkgver-final)
sha256sums=('SKIP')

prepare() {
  cd $pkgname/src

  autoreconf -vfi
}

build() {
  cd $pkgname/src

  # https://web.mit.edu/kerberos/krb5-latest/doc/build/options2configure.html
  ./configure \
    --prefix=/usr \
    --enable-dns-for-realm \
    --disable-rpath \
    --with-system-et \
    --with-system-ss \
    --with-crypto-impl=openssl \
    --with-tls-impl=openssl \
    --with-lmdb

  make
}

package() {
  cd $pkgname/src

  make DESTDIR="$pkgdir" EXAMPLEDIR=/usr/share/doc/$pkgname/examples install

  install -Dpm644 config-files/krb5.conf -t "$pkgdir"/etc/$pkgname/
  install -Dpm644 config-files/kdc.conf -t "$pkgdir"/var/$pkgname/krb5kdc/
}
