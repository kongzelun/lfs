# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=lmdb
pkgver=0.9.31
pkgrel=3
epoch=
pkgdesc="An extraordinarily fast, memory-efficient database"
arch=('x86_64')
url="https://www.symas.com/lmdb"
license=('KZL')
source=(
  git+https://git.openldap.org/openldap/openldap.git#tag=LMDB_$pkgver
  lmdb.pc.in
)
sha256sums=('SKIP'
            'ec9d026f29553bdd9176ce30b2d082349e7a1d03e9e705ff6b45cdfc63a77280')

build() {
  cd openldap/libraries/liblmdb

  make prefix="/usr"
}

check() {
  cd openldap/libraries/liblmdb

  make test
}

package() {
  cd openldap/libraries/liblmdb

  make DESTDIR="$pkgdir" prefix="/usr" install

  install -Dm644 "$srcdir"/lmdb.pc.in "$pkgdir"/usr/lib/pkgconfig/lmdb.pc
  sed -i -e "s|@PACKAGE_VERSION@|$pkgver|" "$pkgdir"/usr/lib/pkgconfig/lmdb.pc
}
