# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tzdata
pkgver=2021e
pkgrel=1
epoch=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="https://www.iana.org/time-zones"
options=(!emptydirs)
source=(https://www.iana.org/time-zones/repository/releases/tzcode$pkgver.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/$pkgname$pkgver.tar.gz{,.asc})
sha256sums=('584666393a5424d13d27ec01183da17703273664742e049d4f62f62dab631775'
            'SKIP'
            '07ec42b737d0d3c6be9c337f8abb5f00554a0f9cc4fcf01a703d69403b6bb2b1'
            'SKIP')
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34')

timezones=('africa' 'antarctica' 'asia' 'australasia' 'europe' 'northamerica' 'southamerica' 'etcetera' 'backward' 'factory')

prepare() {
  sed -i "s:sbin:bin:g" Makefile
}

build() {
  make LFLAGS=$LDFLAGS
}

package() {
  # install tzcode stuff
  make DESTDIR="$pkgdir/" install

  # install tzdata stuff
  ./zic -d "$pkgdir"/usr/share/zoneinfo ${timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/posix ${timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}
  ./zic -b fat -d "$pkgdir"/usr/share/zoneinfo -p America/New_York
  install -Dm444 iso3166.tab zone1970.tab zone.tab -t "$pkgdir"/usr/share/zoneinfo 
  
  # cleanup
  rm "$pkgdir"/etc/localtime
}
