# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tzdata
pkgver=2022g
pkgrel=1
epoch=
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="https://www.iana.org/time-zones"
license=('KZL')
options=(!emptydirs)
source=(https://www.iana.org/time-zones/repository/releases/tzcode$pkgver.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/$pkgname$pkgver.tar.gz{,.asc})
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34')
sha256sums=('9610bb0b9656ff404c361a41f3286da53064b5469d84f00c9cb2314c8614da74'
            'SKIP'
            '4491db8281ae94a84d939e427bdd83dc389f26764d27d9a5c52d782c16764478'
            'SKIP')

_timezones=('africa' 'antarctica' 'asia' 'australasia' 'europe' 'northamerica' 'southamerica' 'etcetera' 'backward' 'factory')

prepare() {
  sed -i "s:sbin:bin:g" Makefile
}

build() {
  make LFLAGS="$LDFLAGS"
}

package() {
  # install tzcode stuff
  make DESTDIR="$pkgdir/" install

  # install tzdata stuff
  ./zic -d "$pkgdir"/usr/share/zoneinfo ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/posix ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/right -L leapseconds ${_timezones[@]}
  ./zic -b fat -d "$pkgdir"/usr/share/zoneinfo -p US/Central

  # cleanup
  rm "$pkgdir"/etc/localtime
}
