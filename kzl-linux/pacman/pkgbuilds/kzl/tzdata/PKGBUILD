# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tzdata
pkgver=2023c
pkgrel=7
epoch=
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="https://www.iana.org/time-zones"
license=('KZL')
options=(!emptydirs)
source=(https://www.iana.org/time-zones/repository/releases/tzcode$pkgver.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/$pkgname$pkgver.tar.gz{,.asc})
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34')
sha256sums=('46d17f2bb19ad73290f03a203006152e0fa0d7b11e5b71467c4a823811b214e7'
            'SKIP'
            '3f510b5d1b4ae9bb38e485aa302a776b317fb3637bdb6404c4adf7b6cadd965c'
            'SKIP')

_timezones=('africa' 'antarctica' 'asia' 'australasia' 'europe' 'northamerica' 'southamerica' 'etcetera' 'backward' 'factory')

build() {
  make LFLAGS="$LDFLAGS"
}

package() {
  # install tzcode stuff
  make DESTDIR="$pkgdir/" install

  # install tzdata stuff
  ./zic -d "$pkgdir"/usr/share/zoneinfo ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/posix ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/right -L leapseconds ${_timezones[@]}
  ./zic -b fat -d "$pkgdir"/usr/share/zoneinfo -p US/Central

  # cleanup
  rm "$pkgdir"/etc/localtime
}
