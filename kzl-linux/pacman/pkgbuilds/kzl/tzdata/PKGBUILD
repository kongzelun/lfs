# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tzdata
pkgver=2025a
pkgrel=7
epoch=
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="https://www.iana.org/time-zones"
license=('KZL')
options=(!emptydirs)
source=(https://www.iana.org/time-zones/repository/releases/tzcode$pkgver.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/$pkgname$pkgver.tar.gz{,.asc})
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34')
sha256sums=('119679d59f76481eb5e03d3d2a47d7870d592f3999549af189dbd31f2ebf5061'
            'SKIP'
            '4d5fcbc72c7c450ebfe0b659bd0f1c02fbf52fd7f517a9ea13fe71c21eb5f0d0'
            'SKIP')

_timezones=('africa' 'antarctica' 'asia' 'australasia' 'europe' 'northamerica' 'southamerica' 'etcetera' 'backward' 'factory')

build() {
  make CC=gcc LFLAGS="$LDFLAGS $LTOFLAGS"
}

package() {
  make DESTDIR="$pkgdir" install

  # install tzdata stuff
  ./zic -d "$pkgdir"/usr/share/zoneinfo ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/posix ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/right -L leapseconds ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo -p US/Central

  # cleanup
  rm "$pkgdir"/etc/localtime
}
