# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=kbd
pkgver=2.5.1
pkgrel=1
epoch=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('KZL')
checkdepends=(check)
source=(git+https://github.com/legionus/kbd.git#tag=v$pkgver)
sha256sums=('SKIP')

# pkgver() {
#   cd $pkgname

#   git describe --tags | sed 's/^v//;s/-/+/g'
# }

prepare() {
  cd $pkgname

  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map
  unlink data/keymaps/i386/qwertz/sr-latin.map
  cp data/keymaps/i386/qwertz/{slovene,sr-latin}.map

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --datadir=/usr/share/kbd \
    --mandir=/usr/share/man

  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" KEYCODES_PROGS=yes RESIZECONS_PROGS=yes install
}
