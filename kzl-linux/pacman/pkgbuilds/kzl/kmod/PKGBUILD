# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=kmod
pkgver=30
pkgrel=15
epoch=
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git"
license=('KZL')
source=(git+$url#tag=v$pkgver
        depmod.script
        depmod.hook
        depmod-search.conf)
sha256sums=('SKIP'
            '31699f1b8b33f1bd61111328375c10d75c587e49c4fee9b7778e664a9354e4b0'
            '98b99f05eb6898690da96a52d82f53235baaafeeeb97d2fa5fbbc0b86eaa927e'
            '1a92bfeae870f61ce814577e69d2a147a9c0caf6aed1131243e4179241fcc4a8')

prepare() {
  cd $pkgname

  touch libkmod/docs/gtk-doc.make

  autoreconf --force --install --symlink
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-manpages \
    --with-zstd \
    --with-xz \
    --with-zlib \
    --with-openssl

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d
  for tool in lsmod rmmod insmod modinfo modprobe depmod; do
    ln -s kmod "$pkgdir"/usr/bin/$tool
  done
  install -Dm644 "$srcdir"/depmod-search.conf "$pkgdir"/usr/lib/depmod.d/search.conf

  install -Dm644 "$srcdir"/depmod.hook "$pkgdir"/usr/share/libalpm/hooks/60-depmod.hook
  install -Dm755 "$srcdir"/depmod.script "$pkgdir"/usr/share/libalpm/scripts/depmod
}
