# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux linux-headers)
pkgver=6.1.35
pkgrel=3
epoch=
pkgdesc="LTS Linux Kernel"
arch=('x86_64')
url="https://www.kernel.org"
license=('KZL')
makedepends=(bc)
options=(!strip)
source=(git+https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git#branch=linux-${pkgver%.*}.y
        config)
sha256sums=('SKIP'
            '7759ac649a0b4cf8d1360d26d7b51c089f25f88593e211cebba3faad47e4f713')

export KBUILD_BUILD_HOST=kzl-linux
export KBUILD_BUILD_USER=kzl
export KBUILD_BUILD_TIMESTAMP="$(date -u '+%Y/%m/%d %T %z')"

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/^v//'
}

prepare() {
  cd $pkgbase

  mkdir build

  cp "$srcdir"/config build/.config

  make O=build nconfig
}

build() {
  cd $pkgbase

  make O=build all
}

package_linux() {
  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)
  local image_name=$(make O=build -s image_name)
  local modulesdir="/usr/lib/modules/$kernelrelease"

  make INSTALL_MOD_PATH="$pkgdir/usr" O=build modules_install

  install -d -m 755 $pkgdir/boot/
  make INSTALL_PATH="$pkgdir/boot" O=build install

  rm "$pkgdir"/usr/lib/modules/$kernelrelease/{build,source}
  ln -sf /usr/src/linux-headers-$kernelrelease "$pkgdir"/usr/lib/modules/$kernelrelease/source
  ln -sf /usr/src/linux-headers-$kernelrelease/build "$pkgdir"/usr/lib/modules/$kernelrelease/build
}

package_linux-headers() {
  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)

  install -d -m 0755 "$pkgdir"/usr/src/
  cp -a "$srcdir/$pkgbase" "$pkgdir"/usr/src/linux-headers-$kernelrelease
}
