# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux linux-headers)
pkgver=5.15.86
pkgrel=1
epoch=1
pkgdesc="LTS Linux Kernel"
arch=('x86_64')
url="https://www.kernel.org"
license=('KZL')
makedepends=(bc)
options=(!strip)
source=($url/pub/linux/kernel/v${pkgver%%.*}.x/$pkgname-$pkgver.tar.{xz,sign}
        config)
sha256sums=('80fcd9efa443502de9e2750f6dfb59e8de43a5d87a6d2be09dca748d79b5f2ee'
            'SKIP'
            '0afee0f0e3a16d1e8bdea644c6aebff516904dfd92d0663237e0d59c4396ae55')
validpgpkeys=('647F28654894E3BD457199BE38DBBDC86092693E')

export KBUILD_BUILD_HOST=kzl-linux
export KBUILD_BUILD_USER=kzl
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $pkgbase-$pkgver

  mkdir build

  cp "$srcdir"/config build/.config

  make O=build nconfig
}

build() {
  cd $pkgbase-$pkgver

  make O=build all
}

package_linux() {
  cd $pkgbase-$pkgver

  local kernelrelease=$(make O=build -s kernelrelease)
  local image_name=$(make O=build -s image_name)
  local modulesdir="/usr/lib/modules/$kernelrelease"

  make INSTALL_MOD_PATH="$pkgdir/usr" O=build modules_install

  # make INSTALL_PATH="$pkgdir/boot" O=build install

  install -Dm755 build/$image_name "$pkgdir/$modulesdir"/vmlinuz
  install -Dm755 build/$image_name "$pkgdir"/boot/vmlinuz-linux
  install -Dm644 build/System.map -t "$pkgdir/$modulesdir/"
  install -Dm644 build/System.map -t "$pkgdir"/boot/

  rm "$pkgdir"/usr/lib/modules/$kernelrelease/{build,source}
}

package_linux-headers() {
  cd $pkgbase-$pkgver

  local kernelrelease=$(make O=build -s kernelrelease)

  install -d -m 0755 "$pkgdir"/usr/lib/modules/$kernelrelease/
  cp -a . "$pkgdir"/usr/lib/modules/$kernelrelease/source
  mv "$pkgdir"/usr/lib/modules/$kernelrelease/{source/,}build
}
