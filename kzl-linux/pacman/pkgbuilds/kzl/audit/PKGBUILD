# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=audit
pkgver=3.1
pkgrel=1
epoch=
pkgdesc="Userspace components of the audit framework"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/audit"
license=('KZL')
depends=(krb5 libcap-ng)
makedepends=(swig)
backup=(etc/libaudit.conf
        etc/audit/audisp-remote.conf
        etc/audit/auditd.conf
        etc/audit/audit-stop.rules
        etc/audit/plugins.d/af_unix.conf
        etc/audit/plugins.d/au-remote.conf
        etc/audit/plugins.d/syslog.conf)
source=(git+https://github.com/linux-audit/audit-userspace.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd audit-userspace

  ./autogen.sh
}

build() {
  cd audit-userspace

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --disable-zos-remote \
    --enable-gssapi-krb5=yes \
    --enable-systemd=yes \
    --with-python=no \
    --with-python3=no \
    --with-golang=no \
    --with-arm=no \
    --with-aarch64=no \
    --with-apparmor=no \
    --with-io_uring \
    --with-libcap-ng=yes

  make
}

package() {
  cd audit-userspace

  make DESTDIR="$pkgdir/" install

  install -d -m0700 "$pkgdir"/var/log/audit

  rm -rf \
    "$pkgdir"/etc/rc.d \
    "$pkgdir"/etc/sysconfig

  sed -ri 's|/sbin|/usr/bin|' \
    "$pkgdir"/etc/audit/*.conf \
    "$pkgdir"/etc/audit/plugins.d/*.conf \
    "$pkgdir"/usr/lib/systemd/system/auditd.service

  chmod 644 "$pkgdir"/usr/lib/systemd/system/auditd.service
}
