# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=zlib
pkgname=(zlib minizip)
pkgver=1.2.13
pkgrel=2
epoch=
arch=('x86_64')
url="https://zlib.net"
license=('KZL')
source=(git+https://github.com/madler/zlib.git#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd zlib

  CFLAGS+=" -fPIC -ffat-lto-objects"

  ./configure \
    --prefix=/usr

  make

	cd contrib/minizip

	cp Makefile Makefile.orig
	autoreconf --install

	./configure \
    --prefix=/usr \
    --disable-static

	make
}

check() {
  cd zlib

	make test

  cd contrib/minizip
  cp ../README.contrib readme.txt

	make -f Makefile.orig test
}

package_zlib() {
  pkgdesc="A Massively Spiffy Yet Delicately Unobtrusive Compression Library"
  depends=(glibc)

  cd zlib

  make DESTDIR="$pkgdir/" install
}

package_minizip() {
	pkgdesc="Zip and UnZip additionnal library"
  depends=(zlib)

	cd zlib/contrib/minizip

  make DESTDIR="$pkgdir/" install

	# https://github.com/madler/zlib/pull/229
	rm "$pkgdir"/usr/include/minizip/crypt.h
}
