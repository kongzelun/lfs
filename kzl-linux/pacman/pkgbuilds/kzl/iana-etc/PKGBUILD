# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=iana-etc
pkgver=20230104
pkgrel=1
epoch=
pkgdesc="/etc/protocols and /etc/services provided by IANA"
arch=('any')
url="https://www.iana.org/protocols"
license=('KZL')
groups=(base)
backup=(etc/{protocols,services})
source=(https://www.iana.org/assignments/service-names-port-numbers/service-names-port-numbers.xml
        https://www.iana.org/assignments/protocol-numbers/protocol-numbers.xml)
sha256sums=('80ee7145de4106ade9f671abfb37967b4925f3f4ba0dbd0861f4945471c1a85f'
            'b6d36185c2331dee5d6dec6a878d1d4c0c8ed86761dd0cbab5a8faa101d1893b')

package() {
  install -dm755 "$pkgdir"/etc
  install -Dm644 service-names-port-numbers.xml "$pkgdir"/usr/share/iana-etc/service-names-port-numbers.xml
  install -Dm644 protocol-numbers.xml "$pkgdir"/usr/share/iana-etc/protocol-numbers.xml

  awk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/protocol-numbers.xml\n" }
(/<record/) { v=n="" }
(/<value/) { v=$3 }
(/<name/ && $3!~/ /) { n=$3 }
(/<\/record/ && n && v!="") { printf "%-12s %3i %s\n", tolower(n),v,n }
' protocol-numbers.xml > "$pkgdir"/etc/protocols

  awk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/service-names-port-numbers.xml\n" }
(/<record/) { n=u=p=c="" }
(/<name/ && !/\(/) { n=$3 }
(/<number/) { u=$3 }
(/<protocol/) { p=$3 }
(/Unassigned/ || /Reserved/ || /historic/) { c=1 }
(/<\/record/ && n && u && p && !c) { printf "%-15s %5i/%s\n", n,u,p }
' service-names-port-numbers.xml > "$pkgdir"/etc/services
}
