# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=iana-etc
pkgver=20230313
pkgrel=1
epoch=
pkgdesc="/etc/protocols and /etc/services provided by IANA"
arch=('any')
url="https://www.iana.org/protocols"
license=('KZL')
makedepends=(gawk)
backup=(etc/{protocols,services})
source=(protocol-numbers-$pkgver.xml::https://www.iana.org/assignments/protocol-numbers/protocol-numbers.xml
        service-names-port-numbers-$pkgver.xml::https://www.iana.org/assignments/service-names-port-numbers/service-names-port-numbers.xml)
sha256sums=('158d47e0c0dea70f59effe3b20dc7445503ffb496d3e15e847b88d23cbae8c0f'
            '92e07769fef81947c80e89423801a0325d38fafce859e0aea8b092eff54e49c6')

package() {
  install -dm755 "$pkgdir"/etc
  install -Dm644 protocol-numbers-$pkgver.xml "$pkgdir"/usr/share/iana-etc/protocol-numbers.xml
  install -Dm644 service-names-port-numbers-$pkgver.xml "$pkgdir"/usr/share/iana-etc/service-names-port-numbers.xml

  awk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/protocol-numbers.xml\n" }
(/<record/) { v=n="" }
(/<value/) { v=$3 }
(/<name/ && $3!~/ /) { n=$3 }
(/<\/record/ && n && v!="") { printf "%-12s %3i %s\n", tolower(n),v,n }
' protocol-numbers-$pkgver.xml > "$pkgdir"/etc/protocols

  awk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/service-names-port-numbers.xml\n" }
(/<record/) { n=u=p=c="" }
(/<name/ && !/\(/) { n=$3 }
(/<number/) { u=$3 }
(/<protocol/) { p=$3 }
(/Unassigned/ || /Reserved/ || /historic/) { c=1 }
(/<\/record/ && n && u && p && !c) { printf "%-15s %5i/%s\n", n,u,p }
' service-names-port-numbers-$pkgver.xml > "$pkgdir"/etc/services
}
