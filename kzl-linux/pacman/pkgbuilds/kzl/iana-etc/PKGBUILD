# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=iana-etc
pkgver=2024.12.20
pkgrel=5
epoch=
pkgdesc="/etc/protocols and /etc/services provided by IANA"
arch=('any')
url="https://www.iana.org/protocols"
license=('KZL')
backup=(etc/{protocols,services})
source=(
  protocol-numbers-$pkgver.xml::https://www.iana.org/assignments/protocol-numbers/protocol-numbers.xml
  service-names-port-numbers-$pkgver.xml::https://www.iana.org/assignments/service-names-port-numbers/service-names-port-numbers.xml
)
sha256sums=('00f5c66c3ce9d7e8243b5de30a36a6a74bcd3b79b87122df65c1221b153d5bd4'
            '490353310910ad2085f48d29effb8421a7cdfde4df92abf88e6561ede40d46a2')

package() {
  install -dm755 "$pkgdir"/etc
  install -Dm644 protocol-numbers-$pkgver.xml "$pkgdir"/usr/share/iana-etc/protocol-numbers.xml
  install -Dm644 service-names-port-numbers-$pkgver.xml "$pkgdir"/usr/share/iana-etc/service-names-port-numbers.xml

  awk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/protocol-numbers.xml\n" }
(/<record/) { v=n="" }
(/<value/) { v=$3 }
(/<name/ && $3!~/ /) { n=$3 }
(/<\/record/ && n && v!="") { printf "%-12s %3i %s\n", tolower(n),v,n }
' protocol-numbers-$pkgver.xml > "$pkgdir"/etc/protocols

  awk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/service-names-port-numbers.xml\n" }
(/<record/) { n=u=p=c="" }
(/<name/ && !/\(/) { n=$3 }
(/<number/) { u=$3 }
(/<protocol/) { p=$3 }
(/Unassigned/ || /Reserved/ || /historic/) { c=1 }
(/<\/record/ && n && u && p && !c) { printf "%-15s %5i/%s\n", n,u,p }
' service-names-port-numbers-$pkgver.xml > "$pkgdir"/etc/services
}
