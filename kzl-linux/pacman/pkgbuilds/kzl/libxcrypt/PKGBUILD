# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxcrypt
pkgver=4.4.36
pkgrel=2
epoch=
pkgdesc="Modern library for one-way hashing of passwords"
arch=('x86_64')
url="https://github.com/besser82/libxcrypt/"
license=('KZL')
source=(git+https://github.com/besser82/libxcrypt.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-werror \
    --disable-static \
    --disable-failure-tokens \
    --disable-obsolete-api \
    --enable-hashes=strong,glibc

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
