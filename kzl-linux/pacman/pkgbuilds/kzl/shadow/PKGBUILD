# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=shadow
pkgver=4.13
pkgrel=4
epoch=
pkgdesc="Programs for converting UNIX password files to the shadow password format, plus programs for managing user and group accounts"
arch=('x86_64')
url="https://github.com/shadow-maint/shadow"
depends=(acl attr audit libxcrypt pam)
backup=(etc/default/useradd
        etc/login.defs
        etc/pam.d/{chage,chfn,chgpasswd,chpasswd,chsh,groupadd,groupdel,groupmems,groupmod,login,newusers,passwd,useradd,userdel,usermod})
source=(git+https://github.com/shadow-maint/shadow.git#tag=$pkgver
        shadow.{service,timer}
        useradd.defaults)
sha256sums=('SKIP'
            '459e1e5632fe390616e352c8f816ca55fb06731f1235ff25314b5c7447cd5c61'
            'c063768e6cd9b52ec4969796b1c988cc2d8d9f6bdec1e293fc9f87b576d84125'
            'bc9fef974321e399fb47c229a82e37d81a8f39424082ef8e4915d0dbee9ff7c2')

prepare() {
  cd $pkgname

  autoreconf -vfi
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-shadowgrp=yes \
    --enable-man=no \
    --enable-account-tools-setuid=yes \
    --with-audit=yes \
    --with-libpam=yes \
    --with-selinux=no \
    --with-acl=yes \
    --with-attr=yes \
    --with-group-name-max-length=32 \
    --with-su=no \
    --with-fcaps=yes

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install

  # login.defs
  sed -e '/^MOTD_FILE/c\MOTD_FILE' \
      -e '/^#ISSUE_FILE/s/#//' \
      -e '/^ENV_SUPATH/c\ENV_SUPATH PATH=/usr/local/sbin:/usr/local/bin:/usr/bin' \
      -e '/^ENV_PATH/c\ENV_PATH PATH=/usr/local/sbin:/usr/local/bin:/usr/bin' \
      -e '/^UMASK/c\UMASK 077' \
      -e '/^PASS_MIN_LEN/c\PASS_MIN_LEN    4' \
      -e '/^#ENCRYPT_METHOD/c\ENCRYPT_METHOD SHA512' \
      -i "$pkgdir"/etc/login.defs

  # useradd defaults (not provided by upstream)
  install -Dm600 "$srcdir"/useradd.defaults "$pkgdir"/etc/default/useradd

  # systemd units
  install -Dm644 "$srcdir"/shadow.timer "$pkgdir"/usr/lib/systemd/system/shadow.timer
  install -Dm644 "$srcdir"/shadow.service "$pkgdir"/usr/lib/systemd/system/shadow.service
  install -dm755 "$pkgdir"/usr/lib/systemd/system/timers.target.wants
  ln -s ../shadow.timer "$pkgdir"/usr/lib/systemd/system/timers.target.wants/shadow.timer
}
