# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libedit
pkgver=20221030.3.1
pkgrel=7
epoch=
pkgdesc="Command line editor library providing generic line editing, history, and tokenization functions"
arch=('x86_64')
url="https://thrysoee.dk/editline/"
license=('KZL')
source=(https://thrysoee.dk/editline/$pkgname-${pkgver/./-}.tar.gz)
sha256sums=('f0925a5adf4b1bf116ee19766b7daa766917aec198747943b1c4edf67a4be2bb')

build() {
  cd $pkgname-${pkgver/./-}

  ./configure \
    --prefix=/usr

  make V=1
}

check() {
  cd $pkgname-${pkgver/./-}

  make check
}

package() {
  cd $pkgname-${pkgver/./-}

  make DESTDIR="$pkgdir/" install

  rm "$pkgdir"/usr/share/man/man3/history.3
}
