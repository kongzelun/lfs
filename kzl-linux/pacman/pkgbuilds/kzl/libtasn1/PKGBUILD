# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libtasn1
pkgver=4.19.0
pkgrel=1
epoch=
pkgdesc="The ASN.1 library used by GnuTLS, p11-kit"
arch=('x86_64')
url="https://www.gnu.org/software/libtasn1/"
license=('KZL')
# makedepends=(gtk-doc)
source=(git+https://gitlab.com/gnutls/libtasn1.git#tag=v$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  sed -e '/^GTK_DOC_CHECK/d' \
      -e '/doc\/Makefile/d' \
      -e '/doc\/reference\/Makefile/d' \
      -i configure.ac
  sed -e '/--enable-gtk-doc/d' \
      -i Makefile.am

  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-doc \
    --enable-static=no

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
