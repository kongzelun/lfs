# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ncurses
pkgver=6.4
pkgrel=1
epoch=
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64')
url="https://invisible-island.net/ncurses/ncurses.html"
license=('KZL')
depends=(glibc)
makedepends=(autoconf autoconf-archive)
source=(https://ftp.gnu.org/pub/gnu/ncurses/$pkgname-$pkgver.tar.gz)
sha256sums=('6931283d9ac87c5073f30b6290c4c75f21632bb4fc3603ac8100812bed248159')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --without-ada \
    --with-cxx \
    --with-cxx-binding \
    --with-pkg-config-libdir=/usr/lib/pkgconfig \
    --enable-pc-files \
    --with-shared \
    --with-cxx-shared \
    --with-versioned-syms \
    --disable-pkg-ldflags \
    --enable-widec \
    --with-manpage-format=normal \
    --enable-stripping \
    --with-xterm-kbs=del

  make -O
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  for lib in ncurses ncurses++ form menu panel; do
    printf "INPUT(-l%sw)\n" "$lib" > "$pkgdir/usr/lib/lib$lib.so"
    ln -sv $libw.pc "$pkgdir/usr/lib/pkgconfig/$lib.pc"
  done

  printf 'INPUT(-lncursesw)\n' > "$pkgdir/usr/lib/libcursesw.so"
  ln -sv libncurses.so "$pkgdir/usr/lib/libcurses.so"

  for lib in tic tinfo; do
    printf "INPUT(libncursesw.so.%s)\n" "${pkgver:0:1}" > "$pkgdir/usr/lib/lib$lib.so"
    ln -sv libncursesw.so.${pkgver:0:1} "$pkgdir/usr/lib/lib$lib.so.${pkgver:0:1}"
    ln -sv ncursesw.pc "$pkgdir/usr/lib/pkgconfig/$lib.pc"
  done
}
