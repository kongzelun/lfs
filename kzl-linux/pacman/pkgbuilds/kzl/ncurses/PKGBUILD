# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ncurses
pkgver=6.4.20230729
pkgrel=18
epoch=
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64')
url="https://invisible-island.net/ncurses/ncurses.html"
license=('KZL')
source=(https://invisible-mirror.net/archives/$pkgname/current/$pkgname-${pkgver%.*}-${pkgver##*.}.tgz)
sha256sums=('a727680890e4456f4c332dd85ca672822327ad3262ca368d4f22a60079950ba3')

prepare() {
  cd $pkgname-${pkgver%.*}-${pkgver##*.}

  sed -i 's/@EXTRA_PKG_LDFLAGS@ //g' misc/gen-pkgconfig.in misc/ncurses-config.in
}

build() {
  cd $pkgname-${pkgver%.*}-${pkgver##*.}

  ./configure \
    --prefix=/usr \
    --without-ada \
    --with-cxx \
    --with-cxx-binding \
    --with-pkg-config-libdir="/usr/lib/pkgconfig" \
    --enable-pc-files \
    --with-shared \
    --with-cxx-shared \
    --with-versioned-syms \
    --enable-widec \
    --with-manpage-format=normal \
    --enable-stripping \
    --with-xterm-kbs=del

  make
}

package() {
  cd $pkgname-${pkgver%.*}-${pkgver##*.}

  make DESTDIR="$pkgdir" install

  for lib in ncurses ncurses++ form menu panel; do
    printf "INPUT(-l%sw)" "$lib" > "$pkgdir/usr/lib/lib$lib.so"
    ln -sv ${lib}w.pc "$pkgdir/usr/lib/pkgconfig/$lib.pc"
  done

  printf "INPUT(-lncursesw)" > "$pkgdir/usr/lib/libcursesw.so"
  printf "INPUT(-lncurses)" > "$pkgdir/usr/lib/libcurses.so"

  for lib in tic tinfo; do
    printf "INPUT(-lncursesw)" > "$pkgdir/usr/lib/lib$lib.so"
    ln -sv ncursesw.pc "$pkgdir/usr/lib/pkgconfig/$lib.pc"
  done

  install -d -m755 "$pkgdir"/usr/include/ncurses
  install -d -m755 "$pkgdir"/usr/include/ncursesw
  for include in "$pkgdir"/usr/include/*.h; do
    ln -sv ../$(basename $include) "$pkgdir"/usr/include/ncurses
    ln -sv ../$(basename $include) "$pkgdir"/usr/include/ncursesw
  done
}
