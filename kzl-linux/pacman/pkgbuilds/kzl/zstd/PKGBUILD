# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zstd
pkgver=1.5.5
pkgrel=11
epoch=
pkgdesc="Zstandard - Fast real-time compression algorithm"
arch=('x86_64')
url="http://www.zstd.net/"
license=('KZL')
depends=(glibc lz4 xz zlib)
source=(git+https://github.com/facebook/zstd.git#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname/build/cmake

  CFLAGS+=' -ffat-lto-objects'
  CXXFLAGS+=' -ffat-lto-objects'

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DZSTD_BUILD_CONTRIB=ON \
    -DZSTD_BUILD_TESTS=ON \
    -DZSTD_LZ4_SUPPORT=ON \
    -DZSTD_LZMA_SUPPORT=ON \
    -DZSTD_PROGRAMS_LINK_SHARED=ON \
    -DZSTD_ZLIB_SUPPORT=ON \
    -S . \
    -B build \
    -G Ninja

  cmake --build build
}

check() {
  cd $pkgname/build/cmake

  cmake --build build --target test
}

package() {
  cd $pkgname/build/cmake

  DESTDIR="$pkgdir" cmake --build build --target install
}
