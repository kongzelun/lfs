# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=git
pkgver=2.40.0
pkgrel=2
epoch=
arch=('x86_64')
url="https://git-scm.com/"
license=('KZL')
depends=(curl expat pcre2)
# makedepends=(xmlto)
# optdepends=("tk: gitk and git gui"
#             "libsecret: libsecret credential helper")
install=$pkgname.install
source=(git+https://github.com/git/git.git#tag=v$pkgver
        $pkgname.install)
sha256sums=('SKIP'
            'e5676ce50dccf08536f0e89ef4d29f886f07137d186f9b878112f0d055bcfb68')

prepare() {
  cd $pkgname

  make configure
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --with-openssl \
    --with-libpcre2 \
    --with-curl \
    --with-expat \
    --with-editor=/usr/bin/nano \
    --without-tcltk


  make all

  # make -C contrib/credential/libsecret
  make -C contrib/diff-highlight
}

check() {
  cd $pkgname

  make test
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install

  # sysusers
  echo 'u git - "git daemon user" / /usr/bin/git-shell' | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/git.conf

  # bash-completion
  install -Dm0644 contrib/completion/git-completion.bash "$pkgdir"/usr/share/bash-completion/completions/git
  # git prompt
  install -Dm0644 contrib/completion/git-prompt.sh "$pkgdir"/usr/share/git/git-prompt.sh
}
