# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=curl
pkgver=7.82.0
pkgrel=1
epoch=1
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="https://curl.haxx.se"
license=('KZL')
depends=(brotli ca-certificates krb5 libidn2 libnghttp2 libpsl libssh2 openssl)
source=(https://curl.haxx.se/download/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('910cc5fe279dc36e2cca534172c94364cf3fcf7d6494ba56e6c61a390881ddce'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --enable-ipv6 \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --with-openssl \
    --with-brotli \
    --with-gssapi \
    --with-random=/dev/urandom \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --with-libssh2

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  make -C scripts DESTDIR="$pkgdir/" install
}
