# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=curl
pkgver=8.2.0
pkgrel=1
epoch=
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="https://curl.se/"
license=('KZL')
depends=(brotli ca-certificates glibc krb5 libidn2 nghttp2 libpsl libssh2 openldap openssl)
source=(git+https://github.com/curl/curl.git#tag=curl-${pkgver//./_})
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  sed -i "/\WLIBCURL_VERSION\W/s/-DEV//" include/curl/curlver.h

  autoreconf -vfi
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --enable-ipv6 \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --with-openssl \
    --with-brotli \
    --with-gssapi \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --with-ca-path=/etc/ssl/certs \
    --with-libssh2 \
    --with-libidn2 \
    --with-nghttp2

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  make -C scripts DESTDIR="$pkgdir" install
}
