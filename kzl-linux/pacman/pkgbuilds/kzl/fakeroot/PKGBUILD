# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakeroot
pkgver=1.31
pkgrel=15
epoch=
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="https://packages.debian.org/fakeroot"
license=('KZL')
source=(git+https://salsa.debian.org/clint/fakeroot.git#tag=upstream/$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  rm -rf doc
  sed -i '/SUBDIRS=/s/doc //' Makefile.am
  sed -i '/doc\//d' configure.ac

  ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --enable-static=no \
    --with-ipc=sysv

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
