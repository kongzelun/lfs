# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=patch
pkgver=2.7.6.g25
pkgrel=6
epoch=
pkgdesc="A utility to apply patch files to original sources"
arch=('x86_64')
url="https://www.gnu.org/software/patch/"
license=('KZL')
groups=(base-devel)
depends=(ed)
makedepends=(
  autoconf
  automake
  bison
  diffutils
)
source=(
  git+git://git.savannah.gnu.org/patch.git#commit=78ed9decdf54d6185d3d3d1ef8784f5978f81422
  git+https://git.savannah.gnu.org/git/gnulib.git
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/^v//;;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
