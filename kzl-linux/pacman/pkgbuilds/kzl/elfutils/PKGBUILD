# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=elfutils
pkgver=0.188
pkgrel=1
epoch=
pkgdesc="Utilities and libraries to read, create and modify ELF binary files, find and handle DWARF debug data, symbols, thread state and stacktraces for processes and core files"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('KZL')
depends=(bzip2 libcurl libmicrohttpd libarchive sqlite3 xz zlib zstd)
provides=(debuginfod libelf)
options=(staticlibs)
source=(https://sourceware.org/elfutils/ftp/0.188/$pkgname-$pkgver.tar.bz2)
sha256sums=('fb8b0e8d0802005b9a309c60c1d8de32dd2951b56f0c3a3cb56d21ce01595dff')

prepare() {
  cd $pkgname-$pkgver

  # remove failing test
  # https://bugs.archlinux.org/task/74875
  sed -i -e 's/run-backtrace-native.sh //g' tests/Makefile.am
  sed -i -e 's/run-backtrace-native-core.sh //g' tests/Makefile.am

  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver

  CFLAGS+=" -g -ffat-lto-objects"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --program-prefix="eu-" \
    --enable-deterministic-archives

  make -O
}

check() {
  cd $pkgname-$pkgver

  make -O check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
