# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=readline
pkgver=8.1
pkgrel=2
epoch=1
pkgdesc="GNU readline library"
arch=('x86_64')
url="https://tiswww.case.edu/php/chet/readline/rltop.html"
license=('KZL')
provides=(libreadline.so)
backup=(etc/inputrc)
options=(!emptydirs)
source=(
  https://ftp.gnu.org/gnu/readline/readline-$pkgver.tar.gz{,.sig}
  https://ftp.gnu.org/gnu/readline/readline-$pkgver-patches/readline${pkgver//.}-{001..002}{,.sig}
  inputrc
)
sha256sums=('f8ceb4ee131e3232226a17f51b164afc46cd0b9e6cef344be87c65962cb82b02'
            'SKIP'
            '682a465a68633650565c43d59f0b8cdf149c13a874682d3c20cb4af6709b9144'
            'SKIP'
            'e55be055a68cb0719b0ccb5edc9a74edcc1d1f689e8a501525b3bc5ebad325dc'
            'SKIP'
            'bfbb40e8ffd893c37da0f8aaa757936548a91cca0bc899a56e90338990e85e2a')
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB')

prepare() {
  cd $pkgname-$pkgver

  for p in "$srcdir"/readline${pkgver//.}-{001..002}; do
    msg "applying patch $p"
    patch -p0 -i $p
  done

  # remove RPATH from shared objects (FS#14366)
  sed -e 's|-Wl,-rpath,$(libdir) ||g' -i support/shobj-conf
}

build() {
  cd $pkgname-$pkgver

  CFLAGS+="-fPIC"

  ./configure \
    --prefix=/usr

  make SHLIB_LIBS=-lncurses
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir"/inputrc "$pkgdir"/etc/inputrc
}
