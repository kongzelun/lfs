# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=valgrind
pkgver=3.18.1
pkgrel=1
epoch=
pkgdesc="Tool to help find memory-management problems in programs"
arch=('x86_64')
url="http://valgrind.org/"
license=('KZL')
makedepends=(gdb)
checkdepends=(procps-ng)
source=(https://sourceware.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2{,.asc})
sha256sums=('00859aa13a772eddf7822225f4b46ee0d39afbe071d32778da4d99984081f7f5'
            'SKIP')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A')

prepare() {
  cd $pkgname-$pkgver

  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --enable-only64bit \
    --enable-lto \
    --without-mpicc

  make
}

check() {
  cd $pkgname-$pkgver

  ./vg-in-place --error-exitcode=1 /bin/true

  make check CPPFLAGS="" CFLAGS="" CXXFLAGS="" LDFLAGS=""
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
