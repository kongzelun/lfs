# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakechroot
pkgver=2.20.1.7
pkgrel=1
epoch=1
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="https://github.com/dex4er/fakechroot/wiki"
license=('KZL')
source=(git+https://github.com/dex4er/fakechroot.git)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed "s/^master\///;s/-g.*//;s/-/./"
}

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/libfakeroot

  make
}

check() {
  cd $pkgname

  make -k check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
  
  install -dm755 "$pkgdir"/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > "$pkgdir"/etc/ld.so.conf.d/fakechroot.conf
}
