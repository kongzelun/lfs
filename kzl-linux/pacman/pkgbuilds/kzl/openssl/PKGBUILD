# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openssl
pkgver=3.1.0
pkgrel=2
epoch=
pkgdesc="A Open Source Toolkit for the Transport Layer Security (TLS) protocol formerly known as the Secure Sockets Layer (SSL) protocol"
arch=('x86_64')
url="https://www.openssl.org"
license=('KZL')
depends=(glibc)
backup=(etc/ssl/openssl.cnf)
source=(git+https://github.com/openssl/openssl.git#tag=$pkgname-$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  sed -e "s|./demoCA|/etc/ssl|g" \
      -i apps/openssl.cnf apps/CA.pl.in
}

build() {
  cd $pkgname

  # https://github.com/openssl/openssl/blob/master/INSTALL.md#configuration-options

  ./Configure \
    linux-x86_64 \
    --libdir=lib \
    --openssldir=/etc/ssl \
    --prefix=/usr \
    enable-ktls \
    enable-ec_nistp_64_gcc_128 \
    CFLAGS="$CFLAGS" \
    CPPFLAGS="$CPPFLAGS" \
    LDFLAGS="$LDFLAGS"

  make depend
  make
}

check() {
  cd $pkgname

  sed -e "s|/etc/ssl|./demoCA|g" \
      -i apps/openssl.cnf \
      -i apps/CA.pl.in

	make test

  sed -e "s|./demoCA|/etc/ssl|g" \
      -i apps/openssl.cnf \
      -i apps/CA.pl.in
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install_sw install_ssldirs
}
