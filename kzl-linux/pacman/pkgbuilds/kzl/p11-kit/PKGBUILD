# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=p11-kit
pkgver=0.25.5
pkgrel=1
epoch=
pkgdesc="Loads and enumerates PKCS#11 modules"
arch=('x86_64')
url="https://p11-glue.freedesktop.org"
license=('KZL')
depends=(
  libtasn1
)
makedepends=(
  bash-completion
  libffi
)
source=(git+https://github.com/p11-glue/p11-kit.git#tag=$pkgver)
sha256sums=('930b25ef02695046604eab4641626b4fde082157d1baf581f0e5e3732e89de1e')

prepare() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --auto-features enabled \
    --buildtype plain \
    -D bash_completion=enabled \
    -D libffi=enabled \
    -D systemd=enabled \
    -D trust_paths=/etc/pki/ca-trust/source:/usr/share/pki/ca-trust-source \
    build
}

build() {
  cd $pkgname

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  meson install -C build --destdir "$pkgdir"
}
