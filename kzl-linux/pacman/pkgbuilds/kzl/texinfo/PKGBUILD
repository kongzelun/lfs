# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=texinfo
pkgver=7.0.2
pkgrel=1
epoch=
pkgdesc="GNU documentation system for on-line information and printed output"
arch=('x86_64')
url="https://www.gnu.org/software/texinfo/"
license=('KZL')
depends=(ncurses)
source=(git+https://git.savannah.gnu.org/git/texinfo.git#tag=$pkgname-$pkgver
        texinfo-install.hook
        texinfo-remove.hook)
sha256sums=('SKIP'
            'ae3ecf6893d3a38c18905eb2e39ed51fddcbf841474d3af074dfab13c197f3b0'
            '7d31aa15d51d7e8ff463a4397abc17ad903b8d4efb34244acf8a67883e4a9b98')

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/texinfo-{install,remove}.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}
