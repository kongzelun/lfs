# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=texinfo
pkgver=7.0.2
pkgrel=1
epoch=
pkgdesc="GNU documentation system for on-line information and printed output"
arch=('x86_64')
url="https://www.gnu.org/software/texinfo/"
license=('KZL')
depends=(glibc gzip ncurses perl)
source=(https://ftp.gnu.org/gnu/texinfo/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook)
sha256sums=('f211ec3261383e1a89e4555a93b9d017fe807b9c3992fb2dff4871dae6da54ad'
            'SKIP'
            'ae3ecf6893d3a38c18905eb2e39ed51fddcbf841474d3af074dfab13c197f3b0'
            '7d31aa15d51d7e8ff463a4397abc17ad903b8d4efb34244acf8a67883e4a9b98')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/texinfo-{install,remove}.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}
