# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=bzip2
pkgver=1.0.8
pkgrel=10
epoch=
pkgdesc="A high-quality data compression program"
arch=('x86_64')
url="https://sourceware.org/bzip2/index.html"
license=('KZL')
depends=(glibc)
source=(git+git://sourceware.org/git/bzip2.git#tag=$pkgname-$pkgver
        $pkgname.pc)
sha256sums=('SKIP'
            '74fca826067d5e54ca503e9dff96f80ecda17d317ecf99fc5eeec10ec7f08990')

prepare() {
  cd $pkgname

  sed -i 's/$(PREFIX)\/man/$(PREFIX)\/share\/man/g' Makefile

  cp "$srcdir"/bzip2.pc bzip2.pc
  sed "s|@VERSION@|$pkgver|" -i bzip2.pc
}

build() {
  cd $pkgname

  make -f Makefile-libbz2_so
  make
}

package() {
  cd $pkgname

  make PREFIX="$pkgdir/usr" install

  cp -f libbz2.so.$pkgver "$pkgdir"/usr/lib
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so.${pkgver%.*}
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so.${pkgver%%.*}
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so

  install -Dm644 bzip2.pc -t "$pkgdir"/usr/lib/pkgconfig
}
