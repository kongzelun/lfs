# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openblas
pkgver=0.3.20
pkgrel=1
epoch=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('x86_64')
url="https://www.openblas.net/"
license=('KZL')
provides=(blas cblas lapack lapacke)
conflicts=(blas cblas lapack lapacke)
source=($pkgname-$pkgver.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha256sums=('8495c9affc536253648e942908e88e097f2ec7753ede55aca52e5dead3029e3c')

# Makefile.rule
_config=(
  FC=gfortran
  USE_THREAD=1
  USE_OPENMP=1
  NUM_THREADS=64
  NO_STATIC=1
  USE_TLS=1
)

build() {
  cd OpenBLAS-$pkgver

  make ${_config[*]} libs netlib shared
}

check() {
  cd OpenBLAS-$pkgver

  make ${_config[*]} test
}

package() {
  cd OpenBLAS-$pkgver

  make ${_config[*]} PREFIX=/usr DESTDIR="${pkgdir}" install

  # blas
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/libblas.so
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/libblas.so.3
  # cblas
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/libcblas.so
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/libcblas.so.3
  # lapack
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/liblapack.so
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/liblapack.so.3
  # lapacke
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/liblapacke.so
  ln -s libopenblasp-r$pkgver.so "$pkgdir"/usr/lib/liblapacke.so.3

  rmdir "$pkgdir"/usr/bin
}
