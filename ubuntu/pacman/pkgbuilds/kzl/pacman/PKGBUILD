# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman
pkgver=6.0.2
pkgrel=2
epoch=
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('KZL')
backup=(usr/local/etc/pacman.conf
        usr/local/etc/makepkg.conf)
source=(git+https://gitlab.archlinux.org/pacman/pacman.git#tag=v$pkgver)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/^v//;s/-g.*//;s/-/./'
}

build() {
  cd $pkgname

    meson setup \
      --prefix /usr/local \
      --sysconfdir etc \
      --optimization 2 \
      -D b_lto=true \
      -D b_pie=true \
      -D buildstatic=false \
      -D crypto=openssl \
      -D curl=enabled \
      -D doc=disabled \
      -D doxygen=disabled \
      -D file-seccomp=enabled \
      -D gpgme=enabled \
      -D ldconfig=/usr/bin/ldconfig \
      -D pkg-ext='.pkg.tar.zst' \
      -D scriptlet-shell=/usr/bin/bash \
      -D src-ext='.src.tar.zst' \
      -D use-git-version=true \
      build

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build
}

package() {
  cd $pkgname

  meson install -C build --destdir="$pkgdir"

  sed -e '/^CHOST=/s/x86_64-pc-linux-gnu/x86_64-linux-gnu/' \
      -e '/^#CPPFLAGS=/c\CPPFLAGS=""' \
      -e '/^#CFLAGS=/c\CFLAGS="-march=native -O2 -pipe -fno-plt -fexceptions -fstack-clash-protection -fcf-protection -Wp,-D_FORTIFY_SOURCE=2"' \
      -e '/^#CXXFLAGS=/c\CXXFLAGS="-march=native -O2 -pipe -fno-plt -fexceptions -fstack-clash-protection -fcf-protection -Wp,-D_FORTIFY_SOURCE=2 -Wp,-D_GLIBCXX_ASSERTIONS"' \
      -e '/^#LDFLAGS=/c\LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"' \
      -e '/^#RUSTFLAGS=/c\RUSTFLAGS="-C opt-level=2"' \
      -e '/^#MAKEFLAGS=/c\MAKEFLAGS="-j$(nproc) -O"' \
      -e '/^#DEBUG_CFLAGS=/c\DEBUG_CFLAGS="-g -fvar-tracking-assignments"' \
      -e '/^#DEBUG_CXXFLAGS=/c\DEBUG_CXXFLAGS="-g -fvar-tracking-assignments"' \
      -e '/^#DEBUG_RUSTFLAGS=/c\DEBUG_RUSTFLAGS="-C debuginfo=2"' \
      -e '/^#BUILDDIR=/c\BUILDDIR="$HOME/makepkg/build"' \
      -e '/^INTEGRITY_CHECK=/s/(.*)/(sha256)/' \
      -e '/^#PKGDEST=/c\PKGDEST="$HOME/makepkg/packages"' \
      -e '/^#SRCDEST=/c\SRCDEST="$HOME/makepkg/sources"' \
      -e '/^#SRCPKGDEST=/c\SRCPKGDEST="$HOME/makepkg/srcpackages"' \
      -e '/^#LOGDEST=/c\LOGDEST="$HOME/makepkg/makepkglogs"' \
      -e '/^#PACKAGER=/c\PACKAGER="Zelun Kong <zelun.kong@outlook.com>"' \
      -i "$pkgdir"/usr/local/etc/makepkg.conf

  cat >> "$pkgdir"/usr/local/etc/pacman.conf << EOF

[kzl]
SigLevel = Optional TrustAll
Server = file:///home/.repository/\$repo
EOF
}
