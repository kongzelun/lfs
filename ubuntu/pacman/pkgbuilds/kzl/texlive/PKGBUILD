pkgname=texlive
pkgver=70897
_pkgyear=2024
_texmfver=20240312
pkgrel=5
epoch=
pkgdesc="TeX Live"
arch=('x86_64')
url="https://tug.org/texlive/"
license=('KZL')
options=(!strip)
install=$pkgname.install
source=(git+https://github.com/TeX-Live/texlive-source.git#branch=branch$_pkgyear
        git+https://github.com/TeX-Live/installer.git
        http://tug.ctan.org/systems/texlive/tlnet/tlpkg/texlive.tlpdb
        ################################################################################
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-$pkgver-source.tar.xz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/install-tl-unx.tar.gz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-$_texmfver-texmf.tar.xz
        09-texlive-fonts.conf
        $pkgname.install)
noextract=(texlive-$_texmfver-texmf.tar.xz)
sha256sums=('SKIP'
            'SKIP'
            '110da56d1fe378be7264cb24b48ee2c2d1fa9ea3efedef804897bf0cbc0259d6'
            'fe01050544263a517c9a682942edc1cbb150986c34eab3b6755143f072838e93'
            '48aed339c4405573b115c2bbb6aaf8c6a6c38c8e81c99e3664b481692623e203')


platform="$(uname -i)-$(uname -s | tr '[:upper:]' '[:lower:]')"
prefix="/opt/TeXLive"

pkgver() {
  cd $pkgname-source

  git describe --tags | sed 's/^svn//'
}

prepare() {
  cd $pkgname-source

  rm -rf libs/{cairo,freetype2,gd,gmp,graphite2,harfbuzz,icu,libpaper,libpng,mpfr,pixman,potrace,teckit,zlib,zziplib}

  rm -rf build
  mkdir -p build
}

build() {
  cd $pkgname-source/build

  # https://tug.org/texlive/doc/tlbuild.html

  ./../configure -C \
    --prefix="$prefix" \
    --datarootdir='${prefix}' \
    --infodir='${prefix}/texmf-dist/doc/info' \
    --mandir='${prefix}/texmf-dist/doc/man' \
    --disable-all-pkgs \
    --disable-native-texlive-build \
    --enable-multiplatform \
    --enable-web2c \
    --enable-tex \
    --enable-luatex \
    --enable-luajittex \
    --enable-pdftex \
    --enable-pdftex-synctex \
    --enable-synctex \
    --enable-chktex \
    --enable-dvipng \
    --enable-texlive \
    --enable-linked-scripts \
    --enable-shared \
    --disable-static \
    --with-banner-add="/KZL" \
    --with-gs="/usr/bin/gs" \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-teckit \
    --with-system-graphite2 \
    --with-system-zziplib \
    --with-system-mpfr \
    --without-system-mpfi \
    --with-system-gmp \
    --with-system-cairo \
    --with-system-pixman \
    --with-system-gd \
    --with-system-potrace \
    --with-system-freetype2 \
    --with-system-libpng \
    --with-system-libpaper \
    --with-system-zlib \
    --without-system-ptexenc \
    --without-system-kpathsea \
    --without-x

  make
}

package() {
  cd $pkgname-source/build

  local _bindir="$prefix/bin/$("$srcdir"/texlive-source/build-aux/config.guess)"

  make DESTDIR="$pkgdir" install-strip
  make DESTDIR="$pkgdir" texlinks

  ln -sv "$("$srcdir"/texlive-source/build-aux/config.guess)" "$pkgdir/$prefix/bin/$platform"

  ### texmf-dist
  cp -rv "$srcdir"/installer/texmf-dist "$pkgdir/$prefix"

  ### tlpkg
  cp -rv "$srcdir"/installer/tlpkg "$pkgdir/$prefix"/
  mkdir -pv "$pkgdir/$prefix"/tlpkg/{backups,tlpobj}
  cp -v "$srcdir"/texlive.tlpdb "$pkgdir/$prefix"/tlpkg/

  ### tlmgr
  "$pkgdir/$_bindir"/mktexlsr
  "$pkgdir/$_bindir"/updmap-sys --force --syncwithtrees
  "$pkgdir/$_bindir"/tlmgr option repository https://mirror.ctan.org/systems/texlive/tlnet
  "$pkgdir/$_bindir"/tlmgr remove 00texlive.installer
  "$pkgdir/$_bindir"/tlmgr platform remove \
    aarch64-linux \
    amd64-freebsd \
    amd64-netbsd \
    armhf-linux \
    i386-freebsd \
    i386-linux \
    i386-netbsd \
    i386-solaris \
    universal-darwin \
    windows \
    x86_64-cygwin \
    x86_64-darwinlegacy \
    x86_64-linuxmusl \
    x86_64-solaris || :
  "$pkgdir/$_bindir"/tlmgr update --self
  "$pkgdir/$_bindir"/tlmgr remove \
    scheme-bookpub \
    scheme-context \
    scheme-full \
    scheme-gust \
    scheme-infraonly \
    scheme-medium \
    scheme-minimal \
    scheme-small \
    scheme-tetex
  "$pkgdir/$_bindir"/tlmgr remove \
    collection-basic \
    collection-bibtexextra \
    collection-binextra \
    collection-context \
    collection-fontsextra \
    collection-fontsrecommended \
    collection-fontutils \
    collection-formatsextra \
    collection-games \
    collection-humanities \
    collection-langarabic \
    collection-langchinese \
    collection-langcjk \
    collection-langcyrillic \
    collection-langczechslovak \
    collection-langenglish \
    collection-langeuropean \
    collection-langfrench \
    collection-langgerman \
    collection-langgreek \
    collection-langitalian \
    collection-langjapanese \
    collection-langkorean \
    collection-langother \
    collection-langpolish \
    collection-langportuguese \
    collection-langspanish \
    collection-latex \
    collection-latexextra \
    collection-latexrecommended \
    collection-luatex \
    collection-mathscience \
    collection-metapost \
    collection-music \
    collection-pictures \
    collection-plaingeneric \
    collection-pstricks \
    collection-publishers \
    collection-texworks \
    collection-wintools \
    collection-xetex

  # texlive fonts
  install -Dm644 "$srcdir"/09-texlive-fonts.conf -t "$pkgdir"/usr/share/fonts/conf.avail/

  ### PATH
  echo "export PATH=\$PATH:$_bindir" | install -Dm644 /dev/stdin "$pkgdir"/etc/profile.d/texlive.sh

  ### clean
  rm -rfv "$pkgdir/$prefix"/{readme-html.dir,readme-txt.dir}
  rm -fv "$pkgdir/$prefix"/{index.html,LICENSE.*,README*,release-texlive.txt}
  rm -rfv "$pkgdir/$prefix"/texmf-dist/{dvips,psutils,xdvi,xindy}
  rm -rfv "$pkgdir/$prefix"/texmf-dist/scripts/context/stubs/mswin
  find "$pkgdir/$prefix"/texmf-dist/doc -mindepth 1 -maxdepth 1 -type d ! \( -name man -o -name info \) -exec rm -rfv {} +
  rm -fv "$pkgdir/$prefix"/tlpkg/texlive.tlpdb.*
  rm -fv "$pkgdir/$prefix"/tlpkg/backups/*
  rm -fv "$pkgdir/$prefix"/tlpkg/tlpobj/*.tlpobj
  rm -rfv "$pkgdir/$prefix"/tlpkg/installer/{curl,wget,xz}
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.txt
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.exe
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.bat
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.ini
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.gif
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.pl
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.tcl
}
