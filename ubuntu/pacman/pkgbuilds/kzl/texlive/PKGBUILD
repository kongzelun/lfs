pkgname=texlive
pkgver=70897
_pkgyear=2024
_texmfver=20240312
pkgrel=22
epoch=
pkgdesc="TeX Live"
arch=('x86_64')
url="https://tug.org/texlive/"
license=('KZL')
install=$pkgname.install
source=(git+https://github.com/TeX-Live/texlive-source.git#branch=branch$_pkgyear
        git+https://github.com/TeX-Live/installer.git
        http://tug.ctan.org/systems/texlive/tlnet/tlpkg/texlive.tlpdb
        ################################################################################
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-$pkgver-source.tar.xz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/install-tl-unx.tar.gz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/texlive/$_pkgyear/texlive-$_texmfver-texmf.tar.xz
        $pkgname.install)
# noextract=(texlive-$_texmfver-texmf.tar.xz)
sha256sums=('SKIP'
            'SKIP'
            '18d0140c0e1d991dd668d0ab5dcdbc414d1c24b22c7cb0a697d69a5fb1a5ca9c'
            'd597f623d661838f013fdd5670f72262a857ae877b79faa95eba3c046d50aea1')


platform="$(uname -i)-$(uname -s | tr '[:upper:]' '[:lower:]')"
prefix="/opt/TeXLive"

# use `tlmgr check files` to file missing pkgs
installed_pkgs=(
  00texlive.installation
  texlive.infra
  texlive-scripts
)

pkgver() {
  cd $pkgname-source

  git describe --tags | sed 's/^svn//'
}

prepare() {
  cd $pkgname-source

  rm -rf libs/{cairo,freetype2,gd,gmp,graphite2,harfbuzz,icu,libpaper,libpng,mpfr,pixman,potrace,teckit,zlib,zziplib}

  mkdir -p build
}

build() {
  cd $pkgname-source/build

  # https://tug.org/texlive/doc/tlbuild.html

  ./../configure -C \
    --prefix="$prefix" \
    --datarootdir='${prefix}' \
    --infodir='${prefix}/share/info' \
    --mandir='${prefix}/share/man' \
    --disable-all-pkgs \
    --disable-native-texlive-build \
    --enable-multiplatform \
    --enable-web2c \
    --enable-tex \
    --enable-luatex \
    --enable-luajittex \
    --enable-pdftex \
    --enable-pdftex-synctex \
    --enable-synctex \
    --enable-chktex \
    --enable-dvipng \
    --enable-texlive \
    --enable-linked-scripts \
    --enable-shared \
    --disable-static \
    --with-banner-add="/KZL" \
    --with-gs="/usr/bin/gs" \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-teckit \
    --with-system-graphite2 \
    --with-system-zziplib \
    --with-system-mpfr \
    --without-system-mpfi \
    --with-system-gmp \
    --with-system-cairo \
    --with-system-pixman \
    --with-system-gd \
    --with-system-potrace \
    --with-system-freetype2 \
    --with-system-libpng \
    --with-system-libpaper \
    --with-system-zlib \
    --without-system-ptexenc \
    --without-system-kpathsea \
    --without-x

  make
}

package() {
  cd $pkgname-source/build

  local _platform="$("$srcdir"/texlive-source/build-aux/config.guess)"
  local _bindir="$prefix/bin/$_platform"
  local _libdir="$prefix/lib/$_platform"

  export PATH="$pkgdir/$_bindir:$PATH"
  export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$pkgdir/$_libdir"

  make DESTDIR="$pkgdir" install-strip
  make DESTDIR="$pkgdir" texlinks

  ### texmf-dist
  cp -rv "$srcdir"/installer/texmf-dist "$pkgdir/$prefix"
  cp ../texk/texlive/linked_scripts/texlive/mktexlsr.pl "$pkgdir/$prefix"/texmf-dist/scripts/texlive/mktexlsr.pl

  ### tlpkg
  cp -rv "$srcdir"/installer/tlpkg "$pkgdir/$prefix"/
  mkdir -pv "$pkgdir/$prefix"/tlpkg/{backups,tlpobj}
  rm -fv "$pkgdir/$prefix"/tlpkg/texlive.tlpdb
  for pkg in "${installed_pkgs[@]}"; do
      sed -n \
          -e "/^name $pkg$/,/^$/p" \
          -e "/^name $pkg.$platform$/,/^$/p" \
          "$srcdir"/texlive.tlpdb >> "$pkgdir/$prefix"/tlpkg/texlive.tlpdb
  done
  sed -i \
      -e '/^name texlive-scripts$/,/^revision [0-9]\+/{' \
      -e '/^revision [0-9]\+/c\revision '"$pkgver" \
      -e '}' \
      "$pkgdir/$prefix"/tlpkg/texlive.tlpdb

  ### tlmgr
  tlmgr option repository https://mirror.ctan.org/systems/texlive/tlnet
  tlmgr platform remove \
    aarch64-linux \
    amd64-freebsd \
    amd64-netbsd \
    armhf-linux \
    i386-freebsd \
    i386-linux \
    i386-netbsd \
    i386-solaris \
    universal-darwin \
    windows \
    x86_64-cygwin \
    x86_64-darwinlegacy \
    x86_64-linuxmusl \
    x86_64-solaris || :
  tlmgr update --self || :
  # tlmgr install --reinstall --no-depends-at-all texlive-scripts

  ### PATH
  echo "export PATH=\$PATH:$_bindir" | install -Dm644 /dev/stdin "$pkgdir"/etc/profile.d/texlive.sh

  ### clean
  # find "$pkgdir/$prefix"/texmf-dist/doc -mindepth 1 -maxdepth 1 -type d ! \( -name man -o -name info \) -exec rm -rfv {} +
  rm -rfv "$pkgdir/$prefix"/texmf-dist/doc
  rm -fv "$pkgdir/$prefix"/tlpkg/texlive.tlpdb.*
  rm -fv "$pkgdir/$prefix"/tlpkg/backups/*
  rm -fv "$pkgdir/$prefix"/tlpkg/tlpobj/*.tlpobj
  rm -rfv "$pkgdir/$prefix"/tlpkg/installer/{curl,wget,xz}
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.txt
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.exe
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.bat
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.ini
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.gif
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.pl
  rm -fv "$pkgdir/$prefix"/tlpkg/installer/*.tcl
  rm -rfv "$pkgdir/$prefix"/tlpkg/tlperl
}
