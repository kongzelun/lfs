# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux linux-headers)
pkgver=5.15.160
pkgrel=13
epoch=
pkgdesc="LTS Linux Kernel"
arch=('x86_64')
url="https://www.kernel.org"
license=('KZL')
options=(!strip)
source=(git+https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git#branch=linux-${pkgver%.*}.y
        config
        update-kernel.sh)
sha256sums=('SKIP'
            '18d8c918b767fe15b24a1b29cf98bfbc113543c0b10ba8440447c40d956f1414'
            '843f33a7af06b85909f4f61f674f44a8a76086d6a0bf25946773d07530bcc32d')

export KBUILD_BUILD_HOST=Ubuntu
export KBUILD_BUILD_USER=kzl
export KBUILD_BUILD_TIMESTAMP="$(date '+%Y/%m/%d %T %z')"

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/^v//'
}

prepare() {
  cd $pkgbase

  mkdir -p build

  cp "$srcdir"/config build/.config
}

build() {
  cd $pkgbase

  make O=build all
}

package_linux() {
  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)
  local image_name=$(make O=build -s image_name)
  local modulesdir="/usr/lib/modules/$kernelrelease"

  make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 O=build modules_install

  install -d -m 755 $pkgdir/boot/efi/
  install -Dm755 build/$image_name "$pkgdir/$modulesdir/vmlinuz-$pkgver-KZL"
  install -Dm755 build/$image_name "$pkgdir/boot/vmlinuz-$pkgver-KZL"
  install -Dm755 build/$image_name "$pkgdir"/boot/efi/vmlinuz-KZL
  install -Dm644 "$srcdir"/config "$pkgdir"/boot/config-$pkgver-KZL

  rm "$pkgdir"/usr/lib/modules/$kernelrelease/{build,source}
  ln -sf /usr/src/linux-headers-$kernelrelease "$pkgdir"/usr/lib/modules/$kernelrelease/source
  ln -sf /usr/src/linux-headers-$kernelrelease/build "$pkgdir"/usr/lib/modules/$kernelrelease/build

  sed "s/@pkgver@/$pkgver/" "$srcdir"/update-kernel.sh | install -Dm755 /dev/stdin "$pkgdir"/boot/update-kernel.sh
}

package_linux-headers() {
  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)

  install -d -m 0755 "$pkgdir"/usr/src/
  rsync -a \
    --exclude='*.o' \
    --exclude='*.ko*' \
    --exclude='*.a' \
    --exclude='*.o.cmd' \
    --exclude='*.o.d' \
    --exclude='*.so.dbg' \
    --exclude='.tmp_vmlinux.btf' \
    --exclude='.git*' \
    --exclude='.clang-format' \
    --exclude='.cocciconfig' \
    --exclude='.get_maintainer.ignore' \
    --exclude='.mailmap' \
    --exclude='.rustfmt.toml' \
    "$srcdir/$pkgbase"/ "$pkgdir"/usr/src/linux-headers-$kernelrelease
}
