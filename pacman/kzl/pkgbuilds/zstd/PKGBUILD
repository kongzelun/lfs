# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zstd
pkgver=1.5.2
pkgrel=1
epoch=1
pkgdesc="Zstandard - Fast real-time compression algorithm"
arch=('x86_64')
url="https://www.zstd.net/"
license=('KZL')
source=(https://github.com/facebook/zstd/archive/v$pkgver.tar.gz)
sha256sums=('f7de13462f7a82c29ab865820149e778cbfe01087b3a55b5332707abf9db4a6e')

build() {
  cd $pkgname-$pkgver

  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd $pkgname-$pkgver

  make check
  make -C contrib/pzstd test
}

package() {
  cd $pkgname-$pkgver

  make PREFIX="/usr/" DESTDIR="$pkgdir/" install
  install -Dm755 zstdmt "$pkgdir"/usr/bin/zstdmt
  install -Dm755 contrib/pzstd/pzstd "$pkgdir"/usr/bin/pzstd
}
