# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libedit
pkgver=20210910.3.1
pkgrel=1
epoch=1
pkgdesc="Command line editor library providing generic line editing, history, and tokenization functions"
arch=('x86_64')
url="https://thrysoee.dk/editline/"
license=('KZL')
source=(https://thrysoee.dk/editline/$pkgname-${pkgver/./-}.tar.gz)
sha256sums=('6792a6a992050762edcca28ff3318cdb7de37dccf7bc30db59fcd7017eed13c5')

build() {
  cd $pkgname-${pkgver/./-}

  ./configure \
    --prefix=/usr

  make V=1
}

check() {
  cd $pkgname-${pkgver/./-}

  make check
}

package() {
  cd $pkgname-${pkgver/./-}

  make DESTDIR="$pkgdir/" install

  rm "$pkgdir"/usr/share/man/man3/history.3
}
