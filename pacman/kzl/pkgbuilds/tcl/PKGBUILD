# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tcl
pkgver=8.6.12
pkgrel=1
epoch=1
pkgdesc="The Tcl scripting language"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('KZL')
source=(https://downloads.sourceforge.net/sourceforge/tcl/$pkgname$pkgver-src.tar.gz)
sha256sums=('26c995dd0f167e48b11961d891ee555f680c175f7173ff8cb829f4ebcde4c1a6')

prepare() {
  cd $pkgname$pkgver

  # we build the tcl sqlite interface in sqlite-tcl package
  rm -rf pkgs/sqlite3*
}

build() {
  cd $pkgname$pkgver/unix

  ./configure \
    --prefix=/usr \
    --enable-threads \
    --enable-64bit

  make
}

check() {
  cd $pkgname$pkgver/unix

  make test
}

package() {
  cd $pkgname$pkgver/unix

  make DESTDIR="$pkgdir/" install install-private-headers

  ln -sf tclsh${pkgver%.*} "$pkgdir"/usr/bin/tclsh
  ln -sf libtcl${pkgver%.*}.so "$pkgdir"/usr/lib/libtcl.so
  install -Dm644 tcl.m4 -t "$pkgdir"/usr/share/aclocal

  sed -e "s#$srcdir/$pkgname$pkgver/unix#/usr/lib#" \
      -e "s#$srcdir/$pkgname$pkgver/unix#/usr/include#" \
      -i "$pkgdir"/usr/lib/tclConfig.sh
  
  tdbcver=tdbc1.1.3
  sed -e "s#$srcdir/$pkgname$pkgver/unix/pkgs/$tdbcver#/usr/lib/$tdbcver#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$tdbcver/generic#/usr/include#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$tdbcver/library#/usr/lib/$pkgname${pkgver%.*}#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$tdbcver#/usr/include#" \
      -i "$pkgdir"/usr/lib/$tdbcver/tdbcConfig.sh

  itclver=itcl4.2.2
  sed -e "s#$srcdir/$pkgname$pkgver/unix/pkgs/$itclver#/usr/lib/$itclver#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$itclver/generic#/usr/include#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$itclver#/usr/include#" \
      -i "$pkgdir"/usr/lib/$itclver/itclConfig.sh
}
