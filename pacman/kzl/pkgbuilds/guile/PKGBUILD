# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=guile
pkgver=2.2.7
pkgrel=1
epoch=1
pkgdesc="Portable, embeddable Scheme implementation written in C"
arch=('x86_64')
url="https://www.gnu.org/software/guile/"
license=('KZL')
depends=(gc gmp libffi libunistring)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('44b4c5fbbe257ccdebea18420212c9b3e90c3c86a54920d8554039fc6769a007'
            'SKIP')
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5')

prepare() {
  cd $pkgname-$pkgver

  # disable test known to fail: https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=966301
  sed -e '/test-out-of-memory/d' -i test-suite/standalone/Makefile.am
  autoreconf -ifv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  rm "$pkgdir"/usr/lib/libguile-?.?.so.*-gdb.scm
}
