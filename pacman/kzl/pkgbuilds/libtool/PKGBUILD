# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libtool
pkgver=2.4.6+59+gb55b1cc8
pkgrel=1
epoch=1
pkgdesc="A generic library support script"
arch=('x86_64')
url="https://www.gnu.org/software/libtool"
license=('KZL')
provides=(libltdl libtool-multilib)
makedepends=(help2man)
source=(git+https://git.savannah.gnu.org/git/libtool.git#commit=b55b1cc8419b544b5cc3e1c204b2ce85112ed762
        git+https://git.savannah.gnu.org/git/gnulib.git
        gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.gnulib.url "$srcdir"/gnulib
  git config --local submodule.gl-mod/bootstrap.url "$srcdir"/gnulib-bootstrap
  git submodule update

  ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    lt_cv_sys_lib_dlsearch_path_spec="/usr/lib"

  make
}

check() {
  cd $pkgname

  make gl_public_submodule_commit="" check || warning "Check failed."
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
