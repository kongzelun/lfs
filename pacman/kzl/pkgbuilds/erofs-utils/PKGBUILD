# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=erofs-utils
pkgver=1.3
pkgrel=1
epoch=1
pkgdesc="Userspace utilities for linux-erofs file system"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=('KZL')
depends=(lz4)
source=(https://git.kernel.org/pub/scm/linux/kernel/git/xiang/$pkgname.git/snapshot/$pkgname-$pkgver.tar.gz)
sha256sums=('132635740039bbe76d743aea72378bfae30dbf034e123929f5d794198d4c0b12')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
