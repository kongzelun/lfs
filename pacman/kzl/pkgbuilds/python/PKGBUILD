# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=python
pkgver=3.9.6
pkgrel=1
epoch=1
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
url="https://www.python.org/"
license=('KZL')
depends=(expat gdbm libffi libnsl openssl)
provides=(cython meson ninja python-dnspython python-fonttools python-setuptools python-wheel gyp)
conflicts=(cython meson ninja python-dnspython python-fonttools python-setuptools python-wheel gyp)
replaces=(cython meson ninja python-dnspython python-fonttools python-setuptools python-wheel gyp)
makedepends=(mpdecimal sqlite valgrind)
# makedepends=(mpdecimal sqlite tk valgrind)
install=$pkgname.install
source=(https://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.xz{,.asc}
        $pkgname.install)
sha256sums=('397920af33efc5b97f2e0b57e91923512ef89fc5b3c1d21dbfc8c4828ce0108a'
            'SKIP'
            '135b50cf60f9053df02d269b26f84e5a2793b1d9eb8345f5898de33e258325ea')
validpgpkeys=('E3FF2839C048B25C084DEBE9B26995E310250568')

build() {
  cd Python-$pkgver

  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -fno-semantic-interposition"

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-optimizations \
    --enable-loadable-sqlite-extensions \
    --enable-ipv6 \
    --enable-big-digits \
    --with-universal-archs="intel-64" \
    --with-lto \
    --with-tzpath=/usr/share/zoneinfo \
    --with-system-expat \
    --with-system-ffi \
    --with-system-libmpdec \
    --with-dbmliborder=gdbm:ndbm \
    --with-valgrind \
    --with-computed-gotos \
    --with-ensurepip=no

  make EXTRA_CFLAGS="$CFLAGS"
}

package() {
  cd Python-$pkgver

  make DESTDIR="$pkgdir/" EXTRA_CFLAGS="$CFLAGS" install

  ln -s python${pkgver:0:3} "$pkgdir"/usr/bin/python
}
