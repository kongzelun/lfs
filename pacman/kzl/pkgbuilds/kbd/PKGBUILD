# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=kbd
pkgver=2.4.0
pkgrel=1
epoch=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('KZL')
checkdepends=(check)
source=(git+https://git.kernel.org/pub/scm/linux/kernel/git/legion/kbd.git#tag=v$pkgver?signed)
sha256sums=('SKIP')
validpgpkeys=('7F2A3D07298149A0793C9A4EA45ABA544CFFD434')

prepare() {
  cd $pkgname

  ./autogen.sh -f
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/kbd \
    --mandir=/usr/share/man

  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

check() {
  cd $pkgname

  make check || warning "Check failed."
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" KEYCODES_PROGS=yes RESIZECONS_PROGS=yes install
}
