# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nodejs
pkgver=16.6.2
pkgrel=1
epoch=1
pkgdesc="Evented I/O for V8 javascript"
arch=('x86_64')
url="https://nodejs.org/"
license=('KZL')
depends=(icu libnghttp2 libuv openssl)
# depends=(c-ares icu libnghttp2 libuv openssl)
provides=(npm)
conflicts=(npm)
replaces=(npm)
source=(https://github.com/nodejs/node/archive/v$pkgver.tar.gz)
sha256sums=('9b539b1ea5e3fbd173fcbaae97088401b228c36c2076c98d04c73802713bbb73')

build() {
  cd node-$pkgver

  CFLAGS="${CFLAGS/-O2/-O3}"
  CXXFLAGS="${CXXFLAGS/-O2/-O3}"

  ./configure \
    --prefix=/usr \
    --experimental-http-parser \
    --shared-libuv \
    --shared-nghttp2 \
    --shared-openssl \
    --shared-zlib \
    --with-intl=system-icu

  make
}

check() {
  cd node-$pkgver

  make test || warning "Check failed."
}

package() {
  cd node-$pkgver

  make DESTDIR="$pkgdir/" install
}
