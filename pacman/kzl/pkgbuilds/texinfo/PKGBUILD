# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=texinfo
pkgver=6.8
pkgrel=1
epoch=1
pkgdesc="GNU documentation system for on-line information and printed output"
arch=('x86_64')
url="https://www.gnu.org/software/texinfo/"
license=('KZL')
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook)
sha256sums=('8eb753ed28bca21f8f56c1a180362aed789229bd62fff58bf8368e9beb59fec4'
            'SKIP'
            'ae3ecf6893d3a38c18905eb2e39ed51fddcbf841474d3af074dfab13c197f3b0'
            '7d31aa15d51d7e8ff463a4397abc17ad903b8d4efb34244acf8a67883e4a9b98')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/texinfo-{install,remove}.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}
