# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pam
pkgver=1.5.2
pkgrel=1
epoch=1
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
url="http://linux-pam.org"
license=('KZL')
depends=(libtirpc libxcrypt)
source=(https://github.com/linux-pam/linux-pam/releases/download/v$pkgver/Linux-PAM-$pkgver.tar.xz{,.asc})
sha256sums=('e4ec7131a91da44512574268f493c6d8ca105c87091691b8e9b56ca685d4f94d'
            'SKIP')
validpgpkeys=('296D6F29A020808E8717A8842DB5BD89A340AEB7')

build() {
  cd Linux-PAM-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --disable-db

  make
}

check() {
  cd Linux-PAM-$pkgver

  make check
}

package() {
  cd Linux-PAM-$pkgver

  make DESTDIR="$pkgdir/" SCONFIGDIR="/etc/security/" install

  chmod +s "$pkgdir"/usr/bin/unix_chkpwd
  rm -rf "$pkgdir"/usr/share/doc/Linux-PAM/sag-pam_userdb.html
}
