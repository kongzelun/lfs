# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=debootstrap
pkgver=1.0.124
pkgrel=1
epoch=1
pkgdesc="Bootstrap a basic Debian system"
arch=('any')
url="https://tracker.debian.org/pkg/debootstrap"
depends=(wget)
optdepends=("gnupg: check release signatures")
source=(https://salsa.debian.org/installer-team/debootstrap/-/archive/$pkgver/$pkgname-$pkgver.tar.bz2)
sha256sums=('e77aa52be6f74d7cb865575d501f6974721b0e5051c4f8ca8d8d0e10dfbf8251')

prepare() {
  cd $pkgname-$pkgver

  sed -e 's/sbin/bin/g' -i Makefile
  sed -e 's|export PATH|export PATH="/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin"|' -i debootstrap
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
