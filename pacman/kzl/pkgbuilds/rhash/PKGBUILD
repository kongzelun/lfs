# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rhash
pkgver=1.4.2
pkgrel=1
epoch=1
pkgdesc="Utility for verifying hash sums of files"
arch=('x86_64')
url="https://github.com/rhash/RHash"
license=('KZL')
provides=(libp11-kit)
conflicts=(libp11-kit)
replaces=(libp11-kit)
backup=(etc/rhashrc)
source=(https://github.com/rhash/RHash/archive/v$pkgver.tar.gz
        https://github.com/rhash/RHash/releases/download/v$pkgver/v$pkgver.tar.gz.asc)
sha256sums=('600d00f5f91ef04194d50903d3c79412099328c42f28ff43a0bdb777b00bec62'
            'SKIP')
validpgpkeys=('2875F6B1C2D27A4F0C8AF60B2A714497E37363AE')

build() {
  cd RHash-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --extra-cflags="$CFLAGS" \
    --extra-ldflags="$LDFLAGS"

  make
}

check() {
  cd RHash-$pkgver

  make test test-lib
}

package() {
  cd RHash-$pkgver

  make DESTDIR="$pkgdir/" install
  make -C librhash DESTDIR="$pkgdir/" install-lib-headers install-lib-shared install-so-link
}
