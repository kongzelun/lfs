# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zsh
pkgver=5.8.1
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.zsh.org/"
license=('KZL')
depends=(gdbm libcap pcre)
install=$pkgname.install
source=(https://www.zsh.org/pub/zsh-$pkgver.tar.xz{,.asc}
        zprofile)
sha256sums=('b6973520bace600b4779200269b1e5d79e5f505ac4952058c11ad5bbf0dd9919'
            'SKIP'
            '230832038c3b8f67fdb1b284ac5f68d709cdb7f1bc752b0e60657b9b9d091045')
validpgpkeys=('7CA7ECAAF06216B90F894146ACF8146CAE8CBBC4')

prepare() {
  cd $pkgname-$pkgver

	# Set correct keymap path
	sed -e 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' -i Completion/Unix/Command/_loadkeys

	# Fix usb.ids path
	sed -e 's#/usr/share/misc/usb.ids#/usr/share/hwdata/usb.ids#g' -i Completion/Linux/Command/_lsusb
	
	# Remove unneeded and conflicting completion scripts
	for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
		rm -rf Completion/$_fpath
		sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
	done
	rm -f Completion/Linux/Command/_pkgtool
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-zsh-secure-free \
    --enable-etcdir=/etc/zsh \
    --enable-zshenv=/etc/zsh/zshenv \
    --enable-zshrc=/etc/zsh/zshrc \
    --enable-zprofile=/etc/zsh/zprofile \
    --enable-zlogin=/etc/zsh/zlogin \
    --enable-zlogout=/etc/zsh/zlogout \
    --enable-fndir=/usr/share/zsh/functions \
    --enable-function-subdirs \
    --enable-scriptdir=/usr/share/zsh/scripts \
    --enable-maildir-support \
    --enable-pcre \
    --enable-cap \
    --enable-multibyte \
    --with-term-lib='ncursesw' \
    --with-tcsetpgrp

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -D -m644 "$srcdir"/zprofile "$pkgdir"/etc/zsh/zprofile
}
