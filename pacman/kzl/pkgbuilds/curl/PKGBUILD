# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=curl
pkgver=7.81.0
pkgrel=1
epoch=1
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="https://curl.haxx.se"
license=('KZL')
depends=(ca-certificates krb5 libidn2 libnghttp2 libpsl libssh2 openssl)
source=(https://curl.haxx.se/download/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('ac8e1087711084548d788ef18b9b732c8de887457b81f616fc681d1044b32f98'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --enable-ipv6 \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --with-openssl \
    --with-gssapi \
    --with-random=/dev/urandom \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --with-libssh2

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  make -C scripts DESTDIR="$pkgdir/" install
}
