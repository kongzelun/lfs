# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=elfutils
pkgver=0.186
pkgrel=1
epoch=1
pkgdesc="Utilities to handle ELF object files and DWARF debugging information"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('KZL')
provides=(libelf)
conflicts=(libelf)
replaces=(libelf)
options=(staticlibs)
source=(https://sourceware.org/$pkgname/ftp/$pkgver/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('7f6fb9149b1673d38d9178a0d3e0fb8a1ec4f53a9f4c2ff89469609879641177'
            'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A'
              'EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A')

build() {
  cd $pkgname-$pkgver

  # required for test-suite success
  CFLAGS+=" -g"

  ./configure \
    --prefix=/usr \
    --program-prefix="eu-" \
    --enable-deterministic-archives

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
