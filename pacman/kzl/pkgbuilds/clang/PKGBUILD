# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=clang
pkgver=12.0.1
pkgrel=1
epoch=1
pkgdesc="C language family frontend for LLVM"
arch=('x86_64')
url="https://clang.llvm.org/"
license=('KZL')
groups=(llvm-project)
depends=(compiler-rt llvm)
optdepends=("openmp: OpenMP support in clang with -fopenmp")
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/clang-$pkgver.src.tar.xz{,.sig}
        https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/clang-tools-extra-$pkgver.src.tar.xz{,.sig}
        https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/llvm-$pkgver.src.tar.xz{,.sig})
sha256sums=('6e912133bcf56e9cfe6a346fa7e5c52c2cde3e4e48b7a6cc6fcc7c75047da45f'
            'SKIP'
            '65659efdf97dbed70ae0caee989936b731f249dddc46f1cb4225b2f49b232ae5'
            'SKIP'
            '7d9a8405f557cefc5a21bf5672af73903b64749d9bc3a50322239f56f34ffddf'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

_cmake_options=(
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_INSTALL_LIBEXECDIR=lib
    -DCMAKE_AR=/usr/bin/llvm-ar
    -DCMAKE_NM=/usr/bin/llvm-nm
    -DCMAKE_OBJCOPY=/usr/bin/llvm-objcopy
    -DCMAKE_OBJDUMP=/usr/bin/llvm-objdump
    -DCMAKE_RANLIB=/usr/bin/llvm-ranlib
    -DCMAKE_READELF=/usr/bin/llvm-readelf
    -DCMAKE_C_COMPILER=/usr/bin/clang
    -DCMAKE_CXX_COMPILER=/usr/bin/clang++
    -DCMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS"
    -DCMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS"
    -DCMAKE_LINKER=/usr/bin/lld
    -DCMAKE_EXE_LINKER_FLAGS="-fuse-ld=lld"
    -DCMAKE_MODULE_LINKER_FLAGS="-fuse-ld=lld"
    -DCMAKE_SHARED_LINKER_FLAGS="-fuse-ld=lld"
)

_llvm_options=(
    -DLLVM_BUILD_TESTS=ON
    -DLLVM_LINK_LLVM_DYLIB=ON
    -DLLVM_ENABLE_RTTI=ON
)

prepare() {
  cd $pkgname-$pkgver.src

  mv "$srcdir"/clang-tools-extra-$pkgver.src tools/extra
}

build() {
  cd $pkgname-$pkgver.src

  cmake \
    "${_cmake_options[@]}" \
    "${_llvm_options[@]}" \
    -DLLVM_LIT=/usr/bin/lit \
    -DLLVM_MAIN_SRC_DIR="$srcdir/llvm-$pkgver.src" \
    -S . \
    -B build \
    -G Ninja

  cmake --build build
}

check() {
  cd $pkgname-$pkgver.src

  cmake --build build --target tests || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver.src

  DESTDIR="$pkgdir/" cmake --build build --target install

  sed -e 's|libexec|lib/clang|' -i "$pkgdir"/usr/bin/scan-build
  mv "$pkgdir"/usr/libexec/{ccc,c++}-analyzer "$pkgdir"/usr/lib/clang/
  rmdir "$pkgdir"/usr/libexec
}
