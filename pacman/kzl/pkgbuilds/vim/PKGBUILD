# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=vim
pkgver=8.2.3441
pkgrel=1
epoch=1
pkgdesc="Vi Improved, a highly configurable, improved version of the vi text editor"
arch=('x86_64')
url="https://www.vim.org"
# depends=(acl gpm libffi libgcrypt pcre)
depends=(acl libffi libgcrypt pcre)
# makedepends=(lua ruby tcl)
makedepends=(ruby tcl)
optdepends=("ruby: Ruby language support"
            "lua: Lua language support"
            "tcl: Tcl language support")
source=(https://github.com/vim/vim/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3db6c3af32b741c2e618358bbf002cffe9db2ab8d21f9ea277110fce54fec4d2')

# TODO: gpm lua

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib/vim \
    --enable-luainterp=dynamic \
    --enable-perlinterp=dynamic \
    --enable-python3interp=dynamic \
    --enable-tclinterp=dynamic \
    --enable-rubyinterp=dynamic \
    --enable-cscope \
    --disable-netbeans \
    --enable-multibyte \
    --disable-gui \
    --disable-canberra \
    --enable-acl \
    --disable-gpm \
    --with-features=huge \
    --with-compiledby="KZL" \
    --without-x

  make
}

check() {
  cd $pkgname-$pkgver

  make test || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make VIMRCLOC="/etc" DESTDIR="$pkgdir/" install
  
  install -dm755 "$pkgdir"/etc
  touch "$pkgdir"/etc/vimrc
}
