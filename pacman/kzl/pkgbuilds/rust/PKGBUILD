# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rust
pkgver=1.54.0
pkgrel=1
epoch=2
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('KZL')
depends=(curl llvm)
makedepends=(libffi)
optdepends=("lldb: rust-lldb script"
            "gdb: rust-gdb script")
source=(https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz{,.asc})
sha256sums=('ac8511633e9b5a65ad030a1a2e5bdaa841fdfe3132f2baaa52cc04e71c6c6976'
            'SKIP')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE')

prepare() {
  cd rustc-$pkgver-src

  cp config.toml.example config.toml

  sed -e '/^#link-shared = /c\link-shared = true' \
      -e '/^#vendor = /c\vendor = true' \
      -e '/^#extended = /c\extended = true' \
      -e '/^#tools = /s/^#//' \
      -e '/^#prefix = /c\prefix = "/usr"' \
      -e '/^#channel = /c\channel = "stable"' \
      -e '/^#rpath = /c\rpath = false' \
      -e '/^#llvm-config = /c\llvm-config = "/usr/bin/llvm-config"' \
      -e '/^#sanitizers = /c\sanitizers = true' \
      -e '/^#profiler = /c\profiler = true' \
      -i config.toml

  sed -e 's/libexec/lib/' \
      -i src/bootstrap/dist.rs \
      -i src/tools/cargo/src/cargo/ops/registry/auth.rs
}

build() {
  cd rustc-$pkgver-src

  ./x.py -j "$(nproc)" build
}

check() {
  cd rustc-$pkgver-src

  ./x.py test || warning "Check failed."
}

package() {
  cd rustc-$pkgver-src

  DESTDIR="$pkgdir/" ./x.py -j "$(nproc)" install

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft "$pkgdir"/usr/lib "$pkgdir"/usr/lib/rustlib/x86_64-unknown-linux-gnu/lib/*.so

  install -dm755 "$pkgdir"/usr/share/bash-completion/completions/
  mv "$pkgdir"/etc/bash_completion.d/* "$pkgdir"/usr/share/bash-completion/completions/
  rmdir "$pkgdir"/etc/{bash_completion.d,}

  rm "$pkgdir"/usr/lib/rustlib/{components,install.log,manifest-*,rust-installer-version,uninstall.sh}
}
