# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libtirpc
pkgver=1.3.2
pkgrel=1
epoch=1
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('x86_64')
url="http://git.linux-nfs.org/?p=steved/libtirpc.git;a=summary"
license=('KZL')
depends=(krb5)
backup=(etc/netconfig)
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('e24eb88b8ce7db3b7ca6eb80115dd1284abc5ec32a8deccfed2224fc2532b9fd')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconf=/etc

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
