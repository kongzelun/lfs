# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=coreutils
pkgver=9.0
pkgrel=1
epoch=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('x86_64')
url="https://www.gnu.org/software/coreutils/"
license=('KZL')
depends=(acl attr gmp libcap openssl)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ce30acdf4a41bc5bb30dd955e9eaa75fa216b4e3deb08889ed32433c7b3b97ce'
            'SKIP')
validpgpkeys=('6C37DC12121A5006BC1DB804DF6FD971306037D9')

build() {
  cd $pkgname-$pkgver

  ./configure  \
      --prefix=/usr \
      --libexecdir=/usr/lib \
      --enable-no-install-program=groups,hostname,kill,uptime \
      --with-openssl

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
