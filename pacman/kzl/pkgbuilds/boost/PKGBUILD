# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=boost
pkgver=1.78.0
pkgrel=1
epoch=1
pkgdesc="Free peer-reviewed portable C++ source libraries"
arch=('x86_64')
url="https://www.boost.org/"
license=('KZL')
depends=(icu)
makedepends=(openmpi)
optdepends=("openmpi: for mpi support")
provides=(boost-libs)
conflicts=(boost-libs)
replaces=(boost-libs)
source=(https://boostorg.jfrog.io/artifactory/main/release/$pkgver/source/${pkgname}_${pkgver//./_}.tar.gz
        $pkgname-ublas-c++20-iterator.patch::https://github.com/boostorg/ublas/commit/a31e5cffa85f.patch)
sha256sums=('94ced8b72956591c4775ae2207a9763d3600b30d9d7446562c552f0a14a63be7'
            'aa38addb40d5f44b4a8472029b475e7e6aef1c460509eb7d8edf03491dc1b5ee')

# TODO: Support for OpenMPI

prepare() {
  cd ${pkgname}_${pkgver//./_}

  # https://github.com/boostorg/ublas/pull/97
  patch -Np2 -i "$srcdir"/$pkgname-ublas-c++20-iterator.patch
}

build() {
  cd ${pkgname}_${pkgver//./_}

  ./bootstrap.sh \
    --with-toolset=gcc \
    --with-icu \
    --with-python=/usr/bin/python3 \
    --prefix=/usr

  echo "using mpi ;" >> project-config.jam

  ./b2 \
    --build-dir=build \
    --layout=system \
    toolset=gcc \
    variant=release \
    link=shared,static \
    threading=multi \
    runtime-link=shared \
    cflags="$CPPFLAGS $CFLAGS -fPIC -O3" \
    cxxflags="$CPPFLAGS $CXXFLAGS -std=c++14 -fPIC -O3" \
    linkflags="$LDFLAGS" \
    -d+2 \
    -j$(nproc) \
    stage
}

package() {
  cd ${pkgname}_${pkgver//./_}

  ./b2 \
    --prefix="$pkgdir/usr/" \
    --build-dir=build \
    toolset=gcc \
    variant=release \
    link=shared,static \
    threading=multi \
    runtime-link=shared \
    cflags="$CPPFLAGS $CFLAGS -fPIC -O3" \
    cxxflags="$CPPFLAGS $CXXFLAGS -std=c++14 -fPIC -O3" \
    linkflags="$LDFLAGS" \
    -d+2 \
    -j$(nproc) \
    install
}
