# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openmp
pkgver=12.0.1
pkgrel=1
epoch=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('KZL')
depends=(elfutils libffi)
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/$pkgname-$pkgver.src.tar.xz{,.sig})
sha256sums=('60fe79440eaa9ebf583a6ea7f81501310388c02754dbe7dc210776014d06b091'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

prepare() {
	cd $pkgname-$pkgver.src

	mkdir build
}

build() {
	cd $pkgname-$pkgver.src

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS" \
    -DLIBOMP_INSTALL_ALIASES=OFF \
    -S . \
    -B build

  cmake --build build
}

check() {
	cd $pkgname-$pkgver.src

  cmake --build build --target tests || warning "Check failed."
}

package() {
	cd $pkgname-$pkgver.src

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
