# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openmp
pkgver=12.0.1
pkgrel=1
epoch=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('KZL')
groups=(llvm-project)
depends=(elfutils libffi)
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/$pkgname-$pkgver.src.tar.xz{,.sig})
sha256sums=('60fe79440eaa9ebf583a6ea7f81501310388c02754dbe7dc210776014d06b091'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

_cmake_options=(
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_AR=/usr/bin/llvm-ar
    -DCMAKE_NM=/usr/bin/llvm-nm
    -DCMAKE_OBJCOPY=/usr/bin/llvm-objcopy
    -DCMAKE_OBJDUMP=/usr/bin/llvm-objdump
    -DCMAKE_RANLIB=/usr/bin/llvm-ranlib
    -DCMAKE_READELF=/usr/bin/llvm-readelf
    -DCMAKE_C_COMPILER=/usr/bin/clang
    -DCMAKE_CXX_COMPILER=/usr/bin/clang++
    -DCMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS"
    -DCMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS"
    -DCMAKE_LINKER=/usr/bin/lld
    -DCMAKE_EXE_LINKER_FLAGS="-fuse-ld=lld"
    -DCMAKE_MODULE_LINKER_FLAGS="-fuse-ld=lld"
    -DCMAKE_SHARED_LINKER_FLAGS="-fuse-ld=lld"
)

build() {
	cd $pkgname-$pkgver.src

  cmake \
    "${_cmake_options[@]}" \
    -DLIBOMP_INSTALL_ALIASES=OFF \
    -S . \
    -B build \
    -G Ninja

  cmake --build build
}

check() {
	cd $pkgname-$pkgver.src

  cmake --build build --target tests || warning "Check failed."
}

package() {
	cd $pkgname-$pkgver.src

  DESTDIR="$pkgdir/" cmake --build build --target install
}
