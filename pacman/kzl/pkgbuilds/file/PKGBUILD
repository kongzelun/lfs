# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=file
pkgver=5.41
pkgrel=1
epoch=1
pkgdesc="File type identification utility"
arch=('x86_64')
url="https://www.darwinsys.com/file/"
license=('KZL')
depends=(libseccomp)
source=(ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('13e532c7b364f7d57e23dfeea3147103150cb90593a57af86c10e4f6e411603f'
            'SKIP')
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A')

build() {
  cd $pkgname-$pkgver

  # Fix linking libmagic (vfork needs libpthread)
  CFLAGS+=" -pthread"

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/file \
    --enable-libseccomp

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
