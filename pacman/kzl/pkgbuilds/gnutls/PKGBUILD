# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gnutls
pkgver=3.7.3
pkgrel=1
epoch=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('x86_64')
url="https://www.gnutls.org/"
license=('KZL')
depends=(libidn2 libtasn1 libunistring nettle p11-kit)
source=(https://www.gnupg.org/ftp/gcrypt/gnutls/v${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('fc59c43bc31ab20a6977ff083029277a31935b8355ce387b634fa433f8f6c49a'
            'SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-openssl-compatibility \
    --disable-static \
    --enable-guile \
    --with-idn \
    --with-default-trust-store-pkcs11="pkcs11:model=p11-kit-trust;manufacturer=PKCS%2311%20Kit"

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
