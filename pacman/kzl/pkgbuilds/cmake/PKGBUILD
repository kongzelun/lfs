# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=cmake
pkgver=3.19.6
pkgrel=1
epoch=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
url="https://www.cmake.org/"
depends=(curl jsoncpp libarchive libuv rhash shared-mime-info)
# makedepends=(qt5)
# optdepends=("qt5: cmake-gui")
source=(https://www.cmake.org/files/v${pkgver%.*}/$pkgname-$pkgver.tar.gz)
sha256sums=('ec87ab67c45f47c4285f204280c5cde48e1c920cfcfed1555b27fb3b1a1d20ba')

# TODO: --qt-gui

build() {
  cd $pkgname-$pkgver

  export CXXFLAGS+=" $CPPFLAGS"

  ./bootstrap \
    --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
