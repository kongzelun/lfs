pkgname=texlive
# pkgver=20210325
pkgver=58837
pkgrel=1
epoch=1
pkgdesc="TeX Live"
arch=('x86_64')
url="https://www.tug.org/historic/"
depends=(cairo freetype2 gd ghostscript gmp graphite harfbuzz icu gd libjpeg-turbo libpaper libpng libsigsegv mpfr pixman)
# depends=(cairo freetype2 gd ghostscript gmp graphite harfbuzz icu gd libjpeg-turbo libpaper libpng libsigsegv mpfr pixman poppler t1lib zziplib)
provides=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
conflicts=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
replaces=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
options=(!strip)
install=$pkgname.install
source=(#http://ftp.math.utah.edu/pub/tex/historic/systems/$pkgname/${pkgver:0:4}/$pkgname-$pkgver-source.tar.xz
        #http://ftp.math.utah.edu/pub/tex/historic/systems/$pkgname/${pkgver:0:4}/$pkgname-$pkgver-extra.tar.xz
        #http://ftp.math.utah.edu/pub/tex/historic/systems/$pkgname/${pkgver:0:4}/$pkgname-$pkgver-texmf.tar.xz
        # http://ftp.math.utah.edu/pub/tex/historic/systems/$pkgname/${pkgver:0:4}/install-tl-unx.tar.gz
        git+https://github.com/TeX-Live/texlive-source.git#tag=svn$pkgver
        git+https://github.com/TeX-Live/installer.git
        http://tug.ctan.org/systems/texlive/tlnet/tlpkg/texlive.tlpdb
        09-texlive-fonts.conf
        $pkgname.install)
noextract=($pkgname-$pkgver-texmf.tar.xz)
sha256sums=('SKIP'
            'SKIP'
            '3f608fe12855a2ab583f03c8c1aac704ceef5ce4d9a1672b5220185ed4ef7134'
            'fe01050544263a517c9a682942edc1cbb150986c34eab3b6755143f072838e93'
            'e00da3460cd3a5b563ac31218f41c9545bdf1ae6e7fe9806f439bc34df0e7e83')

prepare() {
  # cd $pkgname-$pkgver-source
  cd $pkgname-source

  # FS#27251
  sed -i s/SELFAUTOPARENT/TEXMFROOT/ texk/tex4htk/t4ht.c

  mkdir build
}

build() {
  # cd $pkgname-$pkgver-source/build
  cd $pkgname-source/build

  ../configure \
    --prefix=/opt/TeXLive \
    --datarootdir=/opt/TeXLive \
    --disable-native-texlive-build \
    --disable-multiplatform \
    --disable-t1utils \
    --disable-dump-share \
    --disable-aleph \
    --enable-luatex \
    --disable-bibtexu \
    --disable-psutils \
    --enable-shared \
    --disable-static \
    --with-clisp-runtime=default \
    --with-banner-add="/KZL" \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-graphite2 \
    --without-system-zziplib \
    --without-system-xpdf \
    --without-system-poppler \
    --with-system-mpfr \
    --with-system-gmp \
    --with-system-cairo \
    --with-system-pixman \
    --with-system-gd \
    --with-system-freetype2 \
    --with-system-libpng \
    --with-system-libpaper \
    --with-system-zlib \
    --without-x

  make
}

check() {
  # cd $pkgname-$pkgver-source/build
  cd $pkgname-source/build

  make -k check || warning "Check failed."
}

package() {
  # cd $pkgname-$pkgver-source/build
  cd $pkgname-source/build

  make DESTDIR="$pkgdir/" install-strip
  make DESTDIR="$pkgdir/" texlinks

  # cp -r "$srcdir"/install-tl-$pkgver/tlpkg "$pkgdir"/opt/TeXLive/
  # cp -r "$srcdir"/$pkgname-$pkgver-extra/tlpkg "$pkgdir"/opt/TeXLive/
  # tar -xf "$srcdir"/$pkgname-$pkgver-texmf.tar.xz -C "$pkgdir"/opt/TeXLive --skip-old-files --strip-components=1

  mkdir -p "$pkgdir"/opt/TeXLive/tlpkg/{backups,installer,tlpobj}
  cp -r "$srcdir"/installer/tlpkg/TeXLive "$pkgdir"/opt/TeXLive/tlpkg/
  cp -r "$srcdir"/installer/tlpkg/installer/{config.guess,ctan-mirrors.pl} "$pkgdir"/opt/TeXLive/tlpkg/installer/
  cp "$srcdir"/texlive.tlpdb "$pkgdir"/opt/TeXLive/tlpkg/

  sed -e 's/SELFAUTOPARENT/SELFAUTODIR/g' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/scripts/texlive/tlmgr.pl
  sed -e 's/% original/% TeXLive\/KZL/' \
      -e 's/texlive2021/texlive/g' \
      -e '/^TEXMFROOT/s/SELFAUTOPARENT/SELFAUTODIR/' \
      -e '/^TEXMFLOCAL/s/SELFAUTOGRANDPARENT/SELFAUTODIR/' \
      -i "$pkgdir"/opt/TeXLive/texmf-dist/web2c/texmf.cnf

  echo "export PATH=\$PATH:/opt/TeXLive/bin" | install -Dm644 /dev/stdin "$pkgdir"/etc/profile.d/texlive.sh

  # texlive fonts
  install -dm755 "$pkgdir"/etc/fonts/conf.avail/
  install -Dm644 "$srcdir"/09-texlive-fonts.conf "$pkgdir"/etc/fonts/conf.avail/

  # cleanup
  rm -rf "$pkgdir"/opt/TeXLive/texmf-dist/scripts/context/stubs/mswin/
}
