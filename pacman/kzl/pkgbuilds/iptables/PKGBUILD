# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=iptables
pkgver=1.8.7
pkgrel=1
epoch=2
pkgdesc="Linux kernel packet control tool"
arch=('x86_64')
url="https://www.netfilter.org/projects/iptables/index.html"
license=('KZL')
depends=(libnetfilter_conntrack libnfnetlink libnftnl libpcap)
provides=(iptables-nft)
conflicts=(iptables-nft)
replaces=(iptables-nft)
source=(https://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('c109c96bb04998cd44156622d36f8e04b140701ec60531a10668cfdff5e8d8f0'
            'SKIP')
validpgpkeys=('37D964ACC04981C75500FB9BD55D978A8A1420E4')

prepare() {
  cd $pkgname-$pkgver

  mkdir build

  # use system one
  rm include/linux/types.h
}

build() {
  cd $pkgname-$pkgver/build

  ../configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-devel \
    --enable-libipq \
    --enable-bpf-compiler

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool

  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir/" install
}
