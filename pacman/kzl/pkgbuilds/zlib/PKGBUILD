# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=zlib
pkgname=(zlib minizip)
pkgver=1.2.11
pkgrel=1
epoch=2
arch=('x86_64')
url="https://zlib.net"
options=(staticlibs)
source=($url/zlib-$pkgver.tar.gz{,.asc})
sha256sums=('c3e5e9fdd5004dcb542feda5ee4f0ff0744628baf8ed2dd5d66f8ca1197cb1a1'
            'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

build() {
  cd zlib-$pkgver

  CFLAGS+=" -fPIC -ffat-lto-objects"

  ./configure \
    --prefix=/usr

  make

	cd contrib/minizip

	cp Makefile Makefile.orig
  cp ../README.contrib readme.txt
	autoreconf --install
  
	./configure \
    --prefix=/usr \
    --disable-static

	make
}

check() {
  cd zlib-$pkgver

	make test

  cd contrib/minizip

	make -f Makefile.orig test
}

package_zlib() {
  pkgdesc="Compression library implementing the deflate compression method found in gzip and PKZIP"

  cd zlib-$pkgver

  make DESTDIR="$pkgdir/" install
}

package_minizip() {
	pkgdesc="Mini zip and unzip based on zlib"

	cd zlib-$pkgver/contrib/minizip

  make DESTDIR="$pkgdir/" install

	# https://github.com/madler/zlib/pull/229
	rm "$pkgdir"/usr/include/minizip/crypt.h
}
