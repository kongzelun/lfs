# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=samba
pkgver=4.15.5
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.samba.org"
license=('KZL')
depends=(acl db gnutls gpgme jansson libaio libarchive libldap libtirpc liburing lmdb popt)
# depends=(acl db gnutls gpgme jansson ldb libaio libarchive libldap libtirpc liburing popt talloc tdb tevent)
makedepends=(rpcsvc-proto)
provides=(ldb talloc tdb tevent)
conflicts=(ldb talloc tdb tevent)
replaces=(ldb talloc tdb tevent)
backup=(etc/conf.d/samba
        etc/logrotate.d/samba
        etc/pam.d/samba)
source=(https://www.samba.org/samba/ftp/stable/$pkgname-$pkgver.tar.{gz,asc}
        samba.logrotate
        samba.pam
        samba.tmpfiles)
sha256sums=('69115e33831937ba5151be0247943147765aece658ba743f44741672ad68d17f'
            'SKIP'
            '0b570570ea49f82e6f9da47b1cce3b441859f52de105b54d4d79b873b924623f'
            'a3e28676e9f11136690f6d46ca15b1d548cec2e29db043206ab09582671dc2d5'
            '5398a42ccb36ac3b87cfa42c8050b535efc2ca38ab3b9354a52f4e0fe71b5800')
validpgpkeys=('81F5E2832BD2545A1897B713AA99442FB680B620')

# TODO: cups ceph-libs

# cpan Parse::Yapp::Driver
# pip markdown
# pip dnspython

build() {
  cd $pkgname-$pkgver

  ./configure \
    --with-gpgme \
    --with-systemd \
    --with-ads \
    --with-ldap \
    --disable-cups \
    --disable-iprint \
    --with-acl-support \
    --with-libarchive \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib \
    --bundled-libraries=!popt \
    --disable-rpath-install \
    --enable-fhs \
    --with-pammodulesdir=/usr/lib/security \
    --with-configdir=/etc/samba \
    --with-lockdir=/var/cache/samba \
    --with-piddir=/run \
    --with-sockets-dir=/run/samba \
    --systemd-install-services

    # --bundled-libraries=!ldb,!popt,!talloc,!tdb,!tevent,!pyldb-util,!pytalloc-util \
    # --with-system-mitkrb5

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir"/run

  # sysconfig
  rm -rf "$pkgdir"/etc/sysconfig
  install -dm755 "$pkgdir"/etc/conf.d
  sed -e '/^EnvironmentFile/s/sysconfig/conf.d/' -i "$pkgdir"/usr/lib/systemd/system/*.service
  install -Dm644 "$srcdir"/$pkgname-$pkgver/packaging/systemd/samba.sysconfig "$pkgdir"/etc/conf.d/samba

  # pam
  install -Dm644 "$srcdir"/samba.pam "$pkgdir"/etc/pam.d/samba 

  # tmpfiles
  install -Dm644 "$srcdir"/samba.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/samba.conf

  # logrotate
  install -Dm644 "$srcdir"/samba.logrotate "$pkgdir"/etc/logrotate.d/samba

  # smb.conf example
  install -Dm644 "$srcdir"/samba-$pkgver/examples/smb.conf.default "$pkgdir"/etc/samba/smb.conf.default
}
