# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakeroot
pkgver=1.26
pkgrel=1
epoch=1
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="https://packages.debian.org/fakeroot"
install=$pkgname.install
source=(https://deb.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.gz
        $pkgname.install)
sha256sums=('480a578ffdc5592e73df4c086950d321b4adc78dbdaec56c82e4fe1fb68de8e9'
            '711d023c713fa45e115de91f82ef0bf6f454e35cd9e5e4cd1768e68ebc2659d0')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv

  make
  
  cd doc
  po4a -k 0 --rm-backups --variable 'srcdir=../doc/' po4a/po4a.cfg
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  
  echo "/usr/lib/libfakeroot" | install -Dm644 /dev/stdin "$pkgdir"/etc/ld.so.conf.d/fakeroot.conf
}
