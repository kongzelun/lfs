# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=p11-kit
pkgver=0.24.1
pkgrel=1
epoch=1
pkgdesc="Loads and enumerates PKCS#11 modules"
arch=('x86_64')
url="https://p11-glue.freedesktop.org"
license=('KZL')
depends=(libffi libtasn1)
makedepends=(bash-completion)
source=(https://github.com/p11-glue/p11-kit/releases/download/$pkgver/p11-kit-$pkgver.tar.xz{,.sig})
sha256sums=('d8be783efd5cd4ae534cee4132338e3f40f182c3205d23b200094ec85faaaef8'
            'SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871')

build() {
  cd $pkgname-$pkgver

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype debugoptimized \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source \
    build

  meson compile -C build
}

check() {
  cd $pkgname-$pkgver

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir/" meson install -C build
}
