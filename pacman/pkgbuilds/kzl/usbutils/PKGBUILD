# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=usbutils
pkgver=014
pkgrel=1
epoch=1
pkgdesc="USB Device Utilities"
arch=('x86_64')
license=('KZL')
depends=(libusb)
source=(https://www.kernel.org/pub/linux/utils/usb/usbutils/$pkgname-$pkgver.tar{.xz,.sign})
sha256sums=('3a079cfad60560227b67192482d7813bf96326fcbb66c04254839715f276fc69'
            'SKIP')
validpgpkeys=('647F28654894E3BD457199BE38DBBDC86092693E')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/hwdata \
    --disable-zlib

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
