# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libtiff
pkgver=4.2.0
pkgrel=1
epoch=1
pkgdesc="Library for manipulation of TIFF images"
arch=('x86_64')
url="http://www.simplesystems.org/libtiff/"
depends=(libjpeg-turbo)
# makedepends=(freeglut glu jbigkit)
# optdepends=('freeglut: for using tiffgt')
source=(https://download.osgeo.org/libtiff/tiff-$pkgver.tar.gz{,.sig})
sha256sums=('eb0484e568ead8fa23b513e9b0041df7e327f4ee2d22db5a533929dfc19633cb'
            'SKIP')
validpgpkeys=('EBDFDB21B020EE8FD151A88DE301047DE1198975')

# TODO: OpenGL; jbigkit

build() {
  cd tiff-$pkgver

  ./configure \
    --prefix=/usr \
	  --sysconfdir=/etc \
	  --mandir=/usr/share/man \
    --with-docdir=/usr/share/doc/$pkgname

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd tiff-$pkgver

  make check || warning "Check failed."
}

package() {
  cd tiff-$pkgver

  make DESTDIR="$pkgdir/" install
}
