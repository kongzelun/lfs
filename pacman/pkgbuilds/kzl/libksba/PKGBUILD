# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libksba
pkgver=1.4.0
pkgrel=1
epoch=1
pkgdesc="A CMS and X.509 access library"
arch=('x86_64')
url="https://www.gnupg.org/related_software/libksba/index.html"
depends=(libgpg-error)
source=(https://www.gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('bfe6a8e91ff0f54d8a329514db406667000cb207238eded49b599761bfca41b6'
            'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
