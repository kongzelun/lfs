# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ed
pkgver=1.18
pkgrel=1
epoch=1
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('x86_64')
url="https://www.gnu.org/software/ed/ed.html"
license=('KZL')
options=(!emptydirs)
source=(https://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.lz{,.sig})
sha256sums=('aca8efad9800c587724a20b97aa8fc47e6b5a47df81606feaba831b074462b4f'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    "CPPFLAGS=$CPPFLAGS" \
    "CFLAGS=$CFLAGS" \
    "LDFLAGS=$LDFLAGS"

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
