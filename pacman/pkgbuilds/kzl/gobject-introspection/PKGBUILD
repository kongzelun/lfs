# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gobject-introspection
pkgver=1.70.0
_glibver=2.70.2
pkgrel=1
epoch=1
pkgdesc="Introspection system for GObject-based libraries"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GObjectIntrospection"
license=('KZL')
depends=(glib2)
makedepends=(cairo)
options=(!emptydirs)
provides=(gobject-introspection-runtime)
conflicts=(gobject-introspection-runtime)
replaces=(gobject-introspection-runtime)
source=(git+https://gitlab.gnome.org/GNOME/gobject-introspection.git#tag=$pkgver
        git+https://gitlab.gnome.org/GNOME/glib.git?signed#tag=$_glibver)
sha256sums=('SKIP'
            'SKIP')
validpgpkeys=('923B7025EE03C1C59F42684CF0942E894B2EAFA0')

# pip: mako markdown sphinx

prepare() {
  cd $pkgname

  git cherry-pick -n 827494d6415b696a effb1e09dee263cd
}

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D glib_src_dir="$srcdir/glib" \
    -D gtk_doc=false \
    build

  ninja -C build
}

check() {
  cd $pkgname

  ninja -C build test
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" ninja -C build install
}
