# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libgcrypt
pkgver=1.10.0
pkgrel=1
epoch=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=('x86_64')
url="https://www.gnupg.org"
license=('KZL')
depends=(libgpg-error)
source=(https://gnupg.org/ftp/gcrypt/libgcrypt/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('6a00f5c05caa4c4acc120c46b63857da0d4ff61dc4b4b03933fa8d46013fae81'
            'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA')

prepare() {
  cd $pkgname-$pkgver

  sed -e "s:t-secmem::" -i tests/Makefile.am
  sed -e "s:t-sexp::" -i tests/Makefile.am

  autoreconf -ifv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-padlock-support

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
