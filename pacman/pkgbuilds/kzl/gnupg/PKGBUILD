# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gnupg
pkgver=2.3.4
pkgrel=1
epoch=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
arch=('x86_64')
url="https://www.gnupg.org/"
license=('KZL')
depends=(gnutls libassuan libgcrypt libgpg-error libksba npth pinentry sqlite)
makedepends=(libusb-compat libldap pcsclite)
optdepends=("libusb-compat: scdaemon"
            "libldap: gpg2keys_ldap"
            "pcsclite: scdaemon")
checkdepends=(openssh)
install=$pkgname.install
source=(https://gnupg.org/ftp/gcrypt/gnupg/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('f3468ecafb1d7f9ad7b51fd1db7aebf17ceb89d2efa8a05cf2f39b4d405402ae'
            'SKIP')
validpgpkeys=('AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD')

prepare() {
  cd $pkgname-$pkgver

	rm doc/gnupg.info*

  rm -rf .git

  sed -e '/^\s*beta=yes/s/yes/no/' \
      -e '/^\s*tmp=\"-unknown\"/s/-unknown//' \
      -i autogen.sh

	./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --sysconfdir=/etc \
    --disable-doc \
    --enable-maintainer-mode

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

	ln -s gpg "$pkgdir"/usr/bin/gpg2
	ln -s gpgv "$pkgdir"/usr/bin/gpgv2

  install -Dm644 doc/examples/systemd-user/*.{service,socket} -t "$pkgdir"/usr/lib/systemd/user/
}
