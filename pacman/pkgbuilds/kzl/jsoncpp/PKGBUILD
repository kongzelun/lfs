# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=jsoncpp
pkgver=1.9.5
pkgrel=1
epoch=1
pkgdesc="C++ library for interacting with JSON"
arch=('x86_64')
url="https://github.com/open-source-parsers/jsoncpp"
license=('KZL')
options=(staticlibs)
source=($url/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f409856e5920c18d0c2fb85276e24ee607d2a09b5e7d5f0a371368903c275da2')

build() {
  cd $pkgname-$pkgver

  msg2 "Building shared library..."
  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --default-library shared \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    build

  meson compile -C build

  msg2 "Building static library..."
  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --default-library static \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    build-static

  meson compile -C build-static
}

check() {
  cd $pkgname-$pkgver

  meson test -C build

  cd test

  python runjsontests.py ../build/jsontestrunner
  python rununittests.py ../build/jsoncpp_test
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir/" meson install -C build
  DESTDIR="$pkgdir/" meson install -C build-static
}
