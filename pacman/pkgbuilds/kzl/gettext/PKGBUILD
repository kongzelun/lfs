# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gettext
pkgver=0.21
pkgrel=1
epoch=1
pkgdesc="GNU internationalization library"
arch=('x86_64')
url="https://www.gnu.org/software/gettext/"
license=('KZL')
depends=(acl glib2 icu libunistring libxml2)
# depends=(acl glib2 libcroco libunistring libxml2)
makedepends=(gettext)
options=(!docs)
source=(https://ftp.gnu.org/pub/gnu/gettext/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('c77d0da3102aec9c07f43671e60611ebff89a996ef159497ce8e59d075786b12'
            'SKIP')
validpgpkeys=('9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D')

# TODO: libcroco

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --without-included-gettext \
    --without-included-libunistring \
    --without-included-libxm \
    --without-emacs

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i gettext-{tools,runtime,runtime/libasprintf}/libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
