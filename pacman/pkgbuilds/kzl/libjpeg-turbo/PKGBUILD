# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libjpeg-turbo
pkgver=2.0.6
pkgrel=1
epoch=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
makedepends=(nasm)
provides=(libjpeg)
source=(https://downloads.sourceforge.net/project/libjpeg-turbo/$pkgver/libjpeg-turbo-$pkgver.tar.gz{,.sig})
sha256sums=('d74b92ac33b0e3657123ddcf6728788c90dc84dcb6a52013d758af3c4af481bb'
            'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434')

build() {
  cd $pkgname-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DWITH_JAVA=0 \
    -DWITH_JPEG8=1 \
    -W no-dev \
    -S . \
    -B build

  cmake --build build
}

check() {
  cd $pkgname-$pkgver

  cmake --build build --target test
}

package() {
  cd $pkgname-$pkgver

  cmake --build build --target install -- \
    DESTDIR="$pkgdir/" \
    docdir="/usr/share/doc/$pkgname" \
		exampledir="/usr/share/doc/$pkgname"
}
